
File.import "kindlike-load.iml";
File.import "irrelevance-load.iml";
File.import "misc-load.iml";
File.import "bar-load.iml";
File.import "logic-load.iml";

File.import "weaksum-aux.iml";
grammaron Weaksum;

beginModule "Weaksum";


define /weaksum a b/
/
  union (x : a) . b x
//
  intersect i . forall (a : U i) . (a -> U i) -> U i
/;

unfold /weaksum/.
typecheck.
qed ();



structure Nothing = AfterWeaksum ();



define /pack {a} b x y/
/
  y
//
  intersect i . forall (a : U i) (b : a -> U i) .
    forall (x : a) . b x -> weaksum (x : a) . b x
/;

unfold /pack/.
unfold /weaksum/.
typecheck.
qed ();

recordIrrelevance /pack/ [0, 1, 2];



define /unpack {a b c} y f/
/
  f Irrelevance.unavailable y
//
  intersect i . 
  forall (a : U i) (b : a -> U i) (c : U i) .
    (weaksum (x : a) . b x) 
    -> (parametric (x : a) . b x -> c)
    -> c
/;

unfold /unpack/.
unfold /weaksum/.
inference.
introOf /i a b c y f/.
destruct /y/ /x y/.
unhide.
reduce //.
fold /f Ap x/.
typecheck.
qed ();

recordIrrelevance /unpack/ [0, 1, 2];



reductions
/
  unpack _ _ _ (pack _ _ x y) f --> f Ap x y ;

  unfolding unpack pack paramapp
/;



lemma "weaksum_subtype"
/
  forall i (a a' : U i) (b : a -> U i) (b' : a' -> U i) .
    a <: a'
    -> (forall (x : a) . b x <: b' x)
    -> (weaksum (x : a) . b x) <: (weaksum (x : a') . b' x)
/;

inference.
intro /i a a' b b' Ha Hb/.
intro /y/.
destruct /y/ /x y/.  (* weaksum's destructor is not installed yet *)
unhide.
fold /`pack a' (fn x' . b' x') x y/.
typecheck.
qed ();



lemma "weaksum_eeqtp"
/
  forall i (a a' : U i) (b : a -> U i) (b' : a' -> U i) .
    a <:> a'
    -> (forall (x : a) . b x <:> b' x)
    -> (weaksum (x : a) . b x) <:> (weaksum (x : a') . b' x)
/;

inference.
intro /i a a' b b' Ha/.
so /Ha/ /Hsuba Hsuba'/.
intro /Hb/.
unfold /eeqtp/.
split.
  {
  apply /weaksum_subtype/ >> auto.
  intro /x/.
  exact /Hb x #1/.
  }

  {
  apply /weaksum_subtype/ >> auto.
  intro /x/.
  exact /Hb x #2/.
  }
qed ();



define /unpackt {a b} u t/
/
  weaksum (x : a) . exists (y : b x) .
    `pack a b x y = u : (weaksum (x : a) . b x)
    & t x y
//
  intersect i .
  forall (a : U i) (b : a -> U i) .
    (weaksum (x : a) . b x)
    -> (forall (x : a) . b x -> U i)
    -> U i
/;

unfold /unpackt/.
typecheck.
qed ();



define /unpackt_intro {a b c} x y z/
/
  pack
    (fn x' . exists (y' : b x') . `pack a b x' y' = `pack a b x y : (weaksum (x : a) . b x) & c x' y')
    x
    (y, (), z)
//
  intersect i .
  forall 
    (a : U i) (b : a -> U i) (c : forall (x : a) . b x -> U i)
    (x : a) (y : b x) .
      c x y -> `unpackt a b (`pack a b x y) c
/;

unfold /unpackt_intro/.
unfold /unpackt/.
typecheck.
qed ();

recordIrrelevance /unpackt_intro/ [0, 1, 2, 3];



define /unpackt_elim {a b c d} z w f/
/
  `unpack a 
    (fn x . exists (y : b x) . `pack a b x y = z : (weaksum (x : a) . b x) & c x y)
    (d z)
    w
    (fn x w' . f Ap x (w' #1) (w' #2 #2))
//
  intersect i .
  forall 
    (a : U i) (b : a -> U i) (c : forall (x : a) . b x -> U i)
    (d : (weaksum (x : a) . b x) -> U i)
    (z : weaksum (x : a) . b x) .
      `unpackt a b z c
      -> (parametric (x : a) . forall (y : b x) . c x y -> d (`pack a b x y))
      -> d z
/;

unfold /unpackt_elim/.
unfold /unpackt/.
inference.
introOf /i a b c d z w f/.
reduce //.
typecheck.
reintro /x y/.
reduce /y/.
exact /y #2 #1/.
qed ();



reductions
/
  unpackt_elim _ _ _ _ _ (unpackt_intro _ _ _ x y z) f --> f Ap x y z ;

  unfolding unpackt_elim unpackt_intro paramapp
/;



structure Nothing = AfterWeaksumTools ();



lemma "unpackt_simple"
/
  parametric i (a : U i) (b : a -> U i) .
  forall (e : weaksum (x : a) . b x) .
  parametric (c : forall (x : a) . b x -> U i) .
    (parametric x . forall y . c x y)
    -> `unpackt a b e c
/;

inference.
intro /i a b e c Hall/.
destruct /e/ /x y/.
so /`unpackt_intro a b c x y __/ /H/.
  {
  apply /Hall/.
  }
forceExact /H/.
compat.
compat.
extensionality >> auto.
qed ();



lemma "unpackt_map"
/
  forall i 
    (a : U i)
    (b : a -> U i)
    (c d : forall (x : a) . b x -> U i)
    (w : weaksum (x : a) . b x) .
      (parametric (x : a) . forall (y : b x) . c x y -> d x y)
      -> `unpackt a b w c
      -> `unpackt a b w d
/;

inference.
intro /i a b c d w Himpl Hunpack/.
unfold /unpackt in Hunpack in concl/.
destruct /Hunpack/ /x y Heq Hc/.
witness /pack _ x __/.
reduce //.
exists /y/.
split >> auto.
witness /Himpl Ap x y Hc/.
qed ();



lemma "unpackt_assoc"
/
  forall i (a : U i) (b : a -> U i) (c : U i) (d : c -> U i)
    (e : weaksum (x : a) . b x)
    (f : parametric (x : a) . b x -> weaksum (x : c) . d x)
    (t : forall (x : c) . d x -> U i) .
      `unpackt c d (`unpack a b (weaksum (x : c) . d x) e f) t
      <->
      `unpackt a b e (fn x y . `unpackt c d (f Ap x y) t)
/;

inference.
intro /i a b c d e f t/.
split.
  {
  destruct /e/ /x y/.
  intro /Hunpack/.
  unfold /unpackt in Hunpack in concl/.
  destruct /Hunpack/ /z w Heq Ht/.
  reduce /Heq/.
  witness /pack _ x __/.
  reduce //.
  exists /y/.
  split.
    {
    compat.
    extensionality >> auto.
    }
  rewrite /<- Heq/.
  apply /unpackt_intro z/ >> auto.
  }

  {
  intro /Hunpack/.
  unfold /unpackt in Hunpack in concl/.
  destruct /Hunpack/ /x y Heq Hunpack/.
  reduce /Hunpack/.
  unfold /unpackt in Hunpack/.
  destruct /Hunpack/ /z w Heq' Ht/.
  witness /pack _ z __/.
  reduce //.
  exists /w/.
  split >> auto.
  rewrite /<- Heq/.
  reduce //.
  hyp /Heq'/.
  }
qed ();



lemma "unpackt_commute"
/
  forall i (a : U i) (b : a -> U i) (c : U i) (d : c -> U i)
    (e : forall (x : a) . b x -> forall (u : c) . d u -> U i)
    (s1 : weaksum (x : a) . b x)
    (s2 : weaksum (u : c) . d u) .
      (`unpackt a b s1 (fn x y . `unpackt c d s2 (fn u v . e x y u v)))
      -> `unpackt c d s2 (fn u v . `unpackt a b s1 (fn x y . e x y u v))
/;

inference.
intro /i a b c d e s1 s2/.
unfold /unpackt at all/.
intro /Hunpack/.
destruct /Hunpack/ /x y Heq Hunpack/.
reduce /Hunpack/.
destruct /Hunpack/ /u v Heq' He/.
witness /pack _ u __/.
reduce //.
exists /v/.
split >> auto.
witness /pack _ x __/.
reduce //.
exists /y/.
auto.
qed ();



lemma "unpackt_commute_iff"
/
  forall i (a : U i) (b : a -> U i) (c : U i) (d : c -> U i)
    (e : forall (x : a) . b x -> forall (u : c) . d u -> U i)
    (s1 : weaksum (x : a) . b x)
    (s2 : weaksum (u : c) . d u) .
      (`unpackt a b s1 (fn x y . `unpackt c d s2 (fn u v . e x y u v)))
      <-> 
      (`unpackt c d s2 (fn u v . `unpackt a b s1 (fn x y . e x y u v)))
/;

inference.
intro /i a b c d e s1 s2/.
split >> apply /unpackt_commute/.
qed ();



lemma "bindbart_unpack_assoc"
/
  forall i 
    (a : U i) 
    (b : a -> U i)
    (c : U i)
    (w : weaksum (x : a) . b x)
    (f : parametric (x : a) . b x -> Bar.bar c)
    (P : c -> U i) .
      (bindbart y = (`unpack a b (Bar.bar c) w f) in P y)
      <->
      `unpackt a b w (fn x y . bindbart z = f Ap x y in P z)
/;

inference.
intro /i a b c w f P/.
split.
  {
  destruct /w/ /x y/.
  reduceParam //.
  intro /Hprop/.
  so /`unpackt_intro a b (fn x y . bindbart z = f Ap x y in P z) x y Hprop/ /H/.
  forceExact /H/.
  compat.
  compat.
  extensionality.
  auto.
  }

  {
  intro /Hprop/.
  abstractOver /w/ /concl/.
  goalCaseT
    /| ? @ _ =>
       \(fnc t =>
           witness /`unpackt_elim a b _ \t\ w Hprop __/)\
    /.
  reduceParam //.
  intro /x y Hprop'/.
  hyp /Hprop'/.
  }
qed ();



lemma "iexists_weaksum"
/
  forall i (a : Kind i) (b : a -> U i) .
    (iexists (x : a) . b x) <:> (weaksum (x : a) . b x)
/;

inference.
intro /i a b/.
unfold /weaksum/.
reduce //.
unfold /eeqtp/.
split.
  {
  intro /(x y)/.
  typecheck.
  }

  {
  intro /(x y)/.
  typecheck.
  }
qed ();



lemma "weaksum_kindlike"
/
  forall i (A : U (1 + i)) .
    Kindlike.kindlike i A
    -> exists (Q : (A -> U i) -> U i) .
         forall (B : A -> U i) . Q B <:> (weaksum (x : A) . B x)
/;

inference.
intro /i A Hkindlike/.
so /Kindlike.exists_kindlike _ _ Hkindlike/ /Q HQ/.
exists /Q/.
unfold /weaksum/.
hyp /HQ/.
qed ();


endModule ();
