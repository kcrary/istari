
Namespace.beginModule "Acc";


Namespace.alias (parseIdent /Acc/) Prim.acc;


defineRaw /preacc A R/
/
  mu t . (exists (x : A) . forall (y : A) . R y x -> t)
/;



lemma "preacc_type"
/
  preacc :
  intersect i .
  forall (A : U i) .
    (A -> A -> U i)
    -> U i
/;

inference.
introOf /i A R/.
unfold /preacc/.
typecheck.
qed ();
recordTyping "preacc_type";



defineRaw /wf A R a x/
/
  fix
    (fn wf' a x .
       exists (_ : x = a #1 : A) .
         forall (y : A) (r : R y x) .
           wf' (a #2 y r) y)
    a x
/;



(* Wf recurses on only the last two arguments, so we don't get a nice
   unrolling reduction automatically, so build one.

   (We're committed to the code for wf by the built-in Acc constant.)
*)

val unroll_wf = makeReduction1
/
  wf A R a x
  -->
  ` exists (eq A x (a #1))
      (fn y . ` forall A
                  (fn z . ` forall (R z x)
                              (fn w . wf A R (a #2 z w) z)))
  ;
  unfolding wf
  unrolling fix
/;

Database.setUnroll (parseConstant /wf/) (Reduction.user1 unroll_wf);



lemma "wf_type"
/
  wf :
  intersect i .
  forall (A : U i) (R : A -> A -> U i) .
    preacc A R -> A -> U i
/;

inference.
introOf /i A R a x/.
revert /x/.
unfold /preacc in a/.
induct /a/.
reintro /u a Hu IH/.
intro /x/.
destruct /a/ /x' f/.
unroll /wf/.
typecheck.
qed ();
recordTyping "wf_type";



(* The built-in Acc doesn't use preacc and wf, so we'll rig something up
   to make it appear that it does.
*)

val unfold_acc = Reduction.user1 $ makeReduction1
/
  Acc A R x --> ` exists (preacc A R) (fn y . wf A R y x) ;
  unfolding wf preacc Acc
  unrolling fix
/;



(*
Built-in:

defineRaw /Acc A R x/
/
  exists (a : preacc A R) . wf A R a x
/;

*)



lemma "Acc_type"
/
  Acc :
  intersect i .
  forall (A : U i) (R : A -> A -> U i) .
    A -> U i
/;

inference.
introOf /i A R x/.
reduceUsing /\unfold_acc\/.
typecheck.
qed ();
recordTyping "Acc_type";



lemma "Acc_intro"
/
  forall i (A : U i) (R : A -> A -> U i) (x : A) .
    (forall (y : A) . R y x -> Acc A R y)
    -> Acc A R x
/;

inference.
intro /i A R x Hacc/.
reduceUsing /\unfold_acc\ in Hacc in concl/.
exists /(x , (fn y r . Hacc y r #1))/.
  {
  unrollType /preacc/.
  typecheck.
  }
unroll /wf/.
exists /()/.
intro /y r/.
exact /Hacc y r #2/.
qed ();



lemma "Acc_elim"
/
  forall i (A : U i) (R : A -> A -> U i) (x y : A) .
    Acc A R x
    -> R y x
    -> Acc A R y
/;

inference.
intro /i A R x y Hacc Hyx/.
reduceUsing /\unfold_acc\ in Hacc in concl/.
destruct /Hacc/ /a w/.
unrollType /preacc in a/.
destruct /a/ /x' f/.
unroll /wf in w/.
destruct /w/ /Heq Hwf/.
exists /f y Hyx/.
apply /Hwf/.
qed ();



Constant.setOpacity (parseConstant /Acc/) Constant.OPAQUE;



Namespace.endModule ();
