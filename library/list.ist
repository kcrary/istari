
File.import "datatype-load.iml";
File.import "logic-load.iml";
File.import "miscellaneous-load.iml";
File.import "nat-load.iml";
File.import "option-load.iml";
File.import "partial-load.iml";
File.import "decidable-load.iml";
File.import "list-aux.iml";


beginModule "List";


typedef
/
  datatype
    intersect (i : level) .
    intermediate (a : U i) .
    U i
  of
    list : type =
    | nil : list
    | cons : a -> list -> list
/;

establishUptype /list/;

val list = parseConstant /list/;
val cons = parseConstant /cons/;
val list_uptype = parseConstant /list_uptype/;



writeRegistry /AfterListEnv/
  /[constant]/
  (Collapse (cons));
structure Nothing = AfterList ();



defineRaw /list_case {a b} l mnil mcons/
/
  list_iter a (fn _ . b) mnil (fn h t _ . mcons h t) l
/;



reductions
/
  list_case _ _ (nil) z _ --> z ;
  list_case _ _ (cons h t) _ s --> s h t ;
  unfolding list_case
/;



lemma "list_case_type"
/
  ` list_case :
  intersect (i : level) .
  forall (a b : U i) .
     list a 
     -> b 
     -> (a -> list a -> b)
     -> b
/;

introOf /i a b l mnil mcons/.
destruct /l/ /| h t/.

(* nil *)
{
reduce //.
hypof.
}

(* cons *)
{
reduce //.
typecheck.
}
qed ();
recordTyping "list_case_type";




(* Append *)

define /append {a} l1 l2/
/
  list_iter a (fn _ . list a) l2 (fn h _ t . cons h t) l1
//
  intersect (i : level) .
  forall (a : U i) .
     list a -> list a -> list a
/;

unfold /append/.
typecheck.
qed ();



reductions
/
  append _ (nil) l --> l ;
  append a (cons h t) l --> cons h (append a t l) ;
  unfolding append
/;



lemma "append_id_l"
/
  forall (i : level) (a : U i) (l : list a) .
    ` append a nil l = l : list a
/;

intro /i a l/.
reflexivity.
qed ();



lemma "append_id_r"
/
  forall (i : level) (a : U i) (l : list a) .
    append l nil = l : list a
/;

inference.
intro /i a l/.
induction /l/.

(* nil *)
{
reflexivity.
}

(* cons *)
{
intro /h t IH/.
compat.
hyp /IH/.
}
qed ();



lemma "append_assoc"
/
  forall (i : level) (a : U i) (l1 l2 l3 : list a) .
    append (append l1 l2) l3 = append l1 (append l2 l3) : list a
/;

inference.
intro /i a l1 l2 l3/.
induction /l1/.

(* nil *)
{
reduce //.
reflexivity.
}

(* cons *)
{
intro /h t IH/.
compat.
hyp /IH/.
}
qed ();



lemma "append_cons_assoc"
/
  forall i (a : U i) (x : a) (l1 l2 : list a) .
    append (cons x l1) l2 = x :: append l1 l2 : list a
/;

inference.
intro /i a x l1 l2/.
induction /l1/ >> auto.
qed ();



lemma "append_eq_nil"
/
  forall i (a : U i) (l1 l2 : list a) .
    nil = append l1 l2 : list a
    -> l1 = nil : list a & l2 = nil : list a
/;

inference.
intro /i a l1 l2 Heq/.
destruct /l1/ /| h t/.
1:{
  reduce /Heq/.
  injection /Heq/.
  }
reduce /Heq/.
split >> auto.
qed ();



lemma "append_eq_cons"
/
  forall i (a : U i) (h : a) (t l1 l2 : list a) .
    h :: t = append l1 l2 : list a
    -> (exists l1' .
          l1 = h :: l1' : list a
          & t = append l1' l2 : list a)
       %
       (l1 = nil : list a & l2 = h :: t : list a)
/;

inference.
intro /i a h t l1 l2 Heq/.
destruct /l1/ /| x l1'/.
  {
  right.
  auto.
  }

  {
  left.
  exists /l1'/.
  reduce /Heq/.
  injection /Heq/.
  reintro /Heqh Heqt/.
  auto.
  }
qed ();



(* Length *)

definerec /length {a} l/
/
  `list_case a nat l 0 (fn _ t . succ (length a t))
//
  intersect (i : level) .
  forall (a : U i) . list a -> nat
/;

inference.
introOf /i a l/.
sinduction /l/.
reintro /IH/.
destruct /l/ /| h t/.

(* nil *)
{
unroll /length/.
typecheck.
}

(* cons *)
{
unroll /length/.
typecheck.
apply /IH/.
apply /cons_subterm/.
}
qed ();



reductions
/
  length _ (nil) --> 0 ;
  length a (cons _ t) --> succ (length a t) ;
  unrolling length
/;



lemma "length_append"
/
  forall i (a : U i) (l1 l2 : list a) .
    length (append l1 l2) = length l1 + length l2 : nat
/;

inference.
intro /i a l1 l2/.
induction /l1/.

(* nil *)
{
auto.
}

(* cons *)
{
intro /h t IH/.
compat.
hyp /IH/.
}
qed ();



lemma "length_zero_form"
/
  forall i (a : U i) (l : list a) .
    length l = 0 : nat
    -> l = nil : list a
/;

inference.
intro /i a l H/.
destruct /l/ /| h t/ >> auto.
reduce /H/.
injection /H/.
qed ();



lemma "length_succ_form"
/
  forall i (a : U i) (l : list a) (n : nat) .
    length l = succ n : nat
    -> exists h t . l = h :: t : list a
/;

inference.
intro /i a l n H/.
destruct /l/ /| h t/.

{
reduce /H/.
injection /H/.
}

{
exists /h/.
exists /t/.
auto.
}
qed ();



lemma "length_nonzero_form"
/
  forall i (a : U i) (l : list a) .
    0 < length l
    -> exists h t . l = h :: t : list a
/;

inference.
intro /i a l H/.
destruct /l/ /| h t/.

{
reduce /H/.
so /Nat.lt_0_not _ H/ /0/.
}

{
exists /h/.
exists /t/.
auto.
}
qed ();



lemma "length_leq_form"
/
  forall i (a : U i) (l : list a) n .
    n <= length l
    -> exists (l1 l2 : list a) .
         l = append l1 l2 : list a
         & n = length l1 : nat
/;

inference.
intro /i a l n/.
revert /l/.
induction /n/.

(* 0 *)
{
intro /l _/.
existses [/nil/, /l/].
auto.
}

(* succ *)
{
intro /n IH l Hleq/.
destruct /l/ /| h t/.
  {
  so /Nat.leq_succ_0_not _ Hleq/ /0/.
  }
so /IH t __/ /l1 l2 Heq Hlen/.
  {
  apply /Nat.leq_succ_invert/ >> auto.
  }
existses [/h :: l1/, /l2/].
split.
  {
  reduce //.
  auto.
  }

  {
  reduce //.
  compat >> auto.
  }
}
qed ();



(* Fold *)

define /foldr {a b} z f l/
/
  list_iter a (fn _ . b) z (fn x _ y . f x y) l
//
  intersect i .
  forall (a b : U i) .
    b
    -> (a -> b -> b)
    -> list a
    -> b
/;

introOf /i a b z f l/.
unfold /foldr/.
typecheck.
qed ();



reductions
/
  foldr _ _ z _ (nil) --> z ;
  foldr a b z f (cons h t) --> f h (foldr a b z f t) ;
  unfolding foldr
/;



definerec /foldl {a b} z f l/
/
  `list_case a b l z (fn h t . foldl a b (f h z) f t)
//
  intersect i .
  forall (a b : U i) .
    b
    -> (a -> b -> b)
    -> list a
    -> b
/;

inference.
introOf /i a b z f l/.
revert /z/.
sinduction /l/.
reintro /IH/.
destruct /l/ /| h t/.

(* nil *)
{
intro /z/.
unroll /foldl/.
hypof.
}

(* cons *)
{
intro /z/.
unroll /foldl/.
apply /IH/.
apply /cons_subterm/.
}
qed ();



reductions
/
  foldl _ _ z _ (nil) --> z ;
  foldl a b z f (cons h t) --> foldl a b (f h z) f t ;
  unrolling foldl
/;



lemma "foldr_append"
/
  forall i (a b : U i) (z : b) (f : a -> b -> b) (l1 l2 : list a) .
    foldr z f (append l1 l2) = foldr (foldr z f l2) f l1 : b
/;

inference.
intro /i a b z f l1 l2/.
induction /l1/ >> auto.
qed ();



lemma "foldl_append"
/
  forall i (a b : U i) (z : b) (f : a -> b -> b) (l1 l2 : list a) .
    foldl z f (append l1 l2) = foldl (foldl z f l1) f l2 : b
/;

inference.
intro /i a b z f l1 l2/.
revert /z/.
induction /l1/ >> auto.
qed ();



(* Map *)

define /map {a b} f l/
/
  list_iter a (fn _ . list b) nil (fn h _ t . f h :: t) l
//
  intersect i .
  forall (a b : U i) . (a -> b) -> list a -> list b
/;

introOf /i a b f l/.
unfold /map/.
typecheck.
qed ();



reductions
/
  map _ b _ (nil) --> nil ;
  map a b f (cons h t) --> cons (f h) (map a b f t) ;
  unfolding map
/;



lemma "map_compose"
/
  forall i (a b c : U i) (f : b -> c) (g : a -> b) l .
    map f (map g l) = map (fn x . f (g x)) l : list c
/;

inference.
intro /i a b c f g l/.
induction /l/ >> auto.
qed ();



lemma "map_append"
/
  forall i (a b : U i) (f : a -> b) (l1 l2 : list a) .
    map f (append l1 l2) = append (map f l1) (map f l2) : list b
/;

inference.
intro /i a b f l1 l2/.
induction /l1/ >> auto.
qed ();



lemma "map_as_foldr"
/
  forall i (a b : U i) (f : a -> b) (l : list a) .
    map f l = foldr nil (fn h t . f h :: t) l : list b
/;

inference.
intro /i a b f l/.
induction /l/ >> auto.
qed ();



lemma "length_map"
/
  forall i (a b : U i) (f : a -> b) (l : list a) .
    length (map f l) = length l : nat
/;

inference.
intro /i a b f l/.
induction /l/ >> auto.
qed ();



lemma "foldr_map"
/
  forall i (a b c : U i) (z : c) (f : b -> c -> c) (g : a -> b) (l : list a) .
    foldr z f (map g l) = foldr z (fn h t . f (g h) t) l : c
/;

inference.
intro /i a b c z f g l/.
induction /l/ >> auto.
qed ();



lemma "foldl_map"
/
  forall i (a b c : U i) (z : c) (f : b -> c -> c) (g : a -> b) (l : list a) .
    foldl z f (map g l) = foldl z (fn h t . f (g h) t) l : c
/;

inference.
intro /i a b c z f g l/.
revert /z/.
induction /l/ >> auto.
qed ();



(* Reverse *)

define /reverse {a} l/
/
  list_iter a (fn _ . list a) nil (fn h _ t . append t (h :: nil)) l
//
  intersect i . forall (a : U i) . list a -> list a
/;

inference.
unfold /reverse/.
typecheck.
qed ();



reductions
/
  reverse a (nil) --> nil ;
  reverse a (cons h t) --> append a (reverse a t) (cons h nil) ;
  unfolding reverse
/;



lemma "reverse_as_foldl"
/
  forall i (a : U i) (l : list a) .
    reverse l = foldl nil (fn h t . h :: t) l : list a
/;

inference.
intro /i a/.
assert /forall l1 l2 .
           append (reverse l1) l2 = foldl l2 (fn h t . h :: t) l1 : list a/ /H/.
  {
  inference.
  intro /l1/.
  induction /l1/ >> auto.
  intro /h t IH l2/.
  rewrite /-> append_assoc/.
  reduce //.
  apply /IH/.
  }
intro /l/.
rewrite /<- H/.
rewrite /-> append_id_r/.
auto.
qed ();



lemma "reverse_append"
/
  forall i (a : U i) (l1 l2 : list a) .
    reverse (append l1 l2) = append (reverse l2) (reverse l1) : list a
/;

inference.
intro /i a l1/.
induction /l1/.

(* nil *)
{
intro /l2/.
rewrite /-> append_id_r/.
auto.
}

(* cons *)
{
intro /h t IH l2/.
rewrite /-> IH/.
rewrite /-> append_assoc/.
reflexivity.
}
qed ();



lemma "reverse_invol"
/
  forall i (a : U i) (l : list a) .
    reverse (reverse l) = l : list a
/;

inference.
intro /i a l/.
induction /l/ >> auto.
intro /h t IH/.
rewrite /-> reverse_append/.
reduce //.
auto.
qed ();



lemma "length_reverse"
/
  forall i (a : U i) (l : list a) .
    length (reverse l) = length l : nat
/;

inference.
intro /i a l/.
induction /l/ >> auto.
intro /h t IH/.
rewrite /-> length_append/.
reduce //.
rewrite /-> IH/.
rewrite /-> Nat.plus_commute/.
reflexivity.
qed ();



lemma "foldl_as_foldr"
/
  forall i (a b : U i) (z : b) (f : a -> b -> b) (l : list a) .
    foldl z f l = foldr z f (reverse l) : b
/;

inference.
intro /i a b z f l/.
revert /z/.
induction /l/ >> auto.
intro /h t IH z/.
rewrite /-> IH/.
rewrite /-> foldr_append/.
reduce //.
reflexivity.
qed ();



lemma "foldr_as_foldl"
/
  forall i (a b : U i) (z : b) (f : a -> b -> b) (l : list a) .
    foldr z f l = foldl z f (reverse l) : b
/;

inference.
intro /i a b z f l/.
rewrite /-> foldl_as_foldr/.
rewrite /-> reverse_invol/.
reflexivity.
qed ();



lemma "reverse_map"
/
  forall i (a b : U i) (f : a -> b) l .
    reverse (map f l) = map f (reverse l) : list b
/;

inference.
intro /i a b f l/.
induction /l/ >> auto.
intro /h t IH/.
rewrite /-> map_append/.
reduce //.
auto.
qed ();



(* Forall/Exists/In *)

typedef
/
  datatype
    intersect (i : level) .
    forall (a : U i) (P : a -> U i) .
    U i
  of
    Forall : list a -> type =
    | Forall_nil : Forall nil
    | Forall_cons : forall h t . P h -> Forall t -> Forall (h :: t)
/;



lemma "Forall_uptype"
/
  forall i (a : U i) (P : a -> U i) .
    Partial.uptype a
    -> (forall x . Partial.uptype (P x))
    -> forall l . Partial.uptype (Forall a P l)
/;

inference.
intro /i a P Ha HP/.
apply /Forall_uptype_condition/.
repeat split >> repeat Partiality.proveAdmiss1 >> auto.
qed ();

val cForall = parseConstant /Forall/;
val cForall_uptype = parseConstant /Forall_uptype/;


Database.setImplicits (parseConstant /Forall/) 1;
Database.setImplicits (parseConstant /Forall_nil/) 1;
Database.setImplicits (parseConstant /Forall_cons/) 1;



typedef
/
  datatype
    intersect (i : level) .
    forall (a : U i) (P : a -> U i) .
    U i
  of
    Exists : list a -> type =
    | Exists_hit : forall h t . P h -> Exists (h :: t)
    | Exists_miss : forall h t . Exists t -> Exists (h :: t)
/;


lemma "Exists_uptype"
/
  forall i (a : U i) (P : a -> U i) .
    Partial.uptype a
    -> (forall x . Partial.uptype (P x))
    -> forall l . Partial.uptype (Exists a P l)
/;

inference.
intro /i a P Ha HP/.
apply /Exists_uptype_condition/.
repeat split >> repeat Partiality.proveAdmiss1 >> auto.
qed ();

val cExists = parseConstant /Exists/;
val cExists_uptype = parseConstant /Exists_uptype/;


Database.setImplicits (parseConstant /Exists/) 1;
Database.setImplicits (parseConstant /Exists_hit/) 1;
Database.setImplicits (parseConstant /Exists_miss/) 1;



lemma "Forall_as_foldr"
/
  forall i (a : U i) (P : a -> U i) l .
    Forall P l 
    <->
    foldr unit (fn h Q . P h & Q) l
/;

inference.
intro /i a P l/.
split.
  {
  intro /H/.
  induction /H/ >> auto.
  intro /h t IH Hh Ht/.
  auto.
  }

  {
  induction /l/.
    {
    intro /_/.
    apply /Forall_nil/.
    }
  
    {
    intro /h t IH (Hh Ht)/.
    apply /Forall_cons/ >> auto.
    }
  }
qed ();



lemma "Exists_as_foldr"
/
  forall i (a : U i) (P : a -> U i) l .
    Exists P l 
    <->
    foldr void (fn h Q . P h % Q) l
/;

inference.
intro /i a P l/.
split.
  {
  intro /H/.
  induction /H/ >> auto.
  }

  {
  induction /l/.
    {
    intro /0/.
    }
  
    {
    intro /h t IH (HP | H)/.
      {
      apply /Exists_hit/ >> auto.
      }

      {
      apply /Exists_miss/ >> auto.
      }
    }
  }
qed ();




definerec /In a x l/
/
  `list_case a Miscellaneous.bogus l void (fn h t . x = h : a % In a x t)
//
  intersect i . forall (a : U i) . a -> list a -> U i
/;

introOf /i a x l/.
sinduction /l/.
reintro /IH/.
destruct /l/ /| h t/.
  {
  unroll /In/.
  typecheck.
  }
unroll /In/.
typecheck.
apply /IH/.
apply /cons_subterm/.
qed ();



reductions
/
  In _ _ (nil) --> void ;
  In a x (cons h t) --> x = h : a % In a x t ;
  unrolling In
/;



lemma "In_as_exists"
/
  forall i (a : U i) x l .
    In a x l
    <->
    Exists (fn y . x = y : a) l
/;

inference.
intro /i a x l/.
split.
  {
  induction /l/.
    {
    intro /0/.
    }
  intro /h t IH Hin/.
  destruct /Hin/ /Heq | Hin/ >> autoWith /Exists_hit Exists_miss/.
  }

  {
  intro /H/.
  induction /H/.
    {
    intro /h t Heq/.
    left >> auto.
    }

    {
    intro /h t _ IH/.
    right >> auto.
    }
  }
qed ();



lemma "Forall_forall"
/
  forall i (a : U i) (P : a -> U i) l .
    Forall P l
    <->
    (forall x . In a x l -> P x)
/;

inference.
intro /i a P l/.
split.
  {
  intro /H/.
  induction /H/.
    {
    intro /? 0/.
    }
  intro /h t Hh _ IH x Hin/.
  destruct /Hin/ /Heq | Hin/.
    {
    subst /x/.
    auto.
    }

    {
    apply /IH/ >> auto.
    }
  }

  {
  induction /l/.
    {
    intro /_/.
    apply /Forall_nil/.
    }
  intro /h t IH H/.
  apply /Forall_cons/ >> auto.
  }
qed ();

  

lemma "Exists_exists"
/
  forall i (a : U i) (P : a -> U i) l .
    Exists P l
    <->
    (exists x . In a x l & P x)
/;

inference.
intro /i a P l/.
split.
  {
  intro /H/.
  induction /H/.
    {
    intro /h t Hh/.
    exists /h/.
    auto.
    }

    {
    intro /h t _ IH/.
    destruct /IH/ /x Hin Hx/.
    exists /x/.
    split >> auto.
    }
  }

  {
  induction /l/.
    {
    intro /(? 0 ?)/.
    }
  intro /h t IH (x Hin Hx)/.
  destruct /Hin/ /Heq | Hin/.
    {
    subst /x/.
    apply /Exists_hit/ >> auto.
    }

    {
    apply /Exists_miss/.
    apply /IH/.
    exists /x/.
    auto.
    }
  }
qed ();



lemma "Forall_nil_iff"
/
  forall i (a : U i) (P : a -> U i) .
    Forall P nil
    <->
    unit
/;

inference.
intro /i a P/.
split >> auto.
intro /_/.
apply /Forall_nil/.
qed ();



lemma "Exists_nil_iff"
/
  forall i (a : U i) (P : a -> U i) .
    Exists P nil
    <->
    void
/;

inference.
intro /i a P/.
split.
  {
  intro /H/.
  inversion /H/.
  }

  {
  intro /0/.
  }
qed ();



lemma "Forall_cons_iff"
/
  forall i (a : U i) (P : a -> U i) x l .
    Forall P (x :: l)
    <->
    P x & Forall P l
/;

inference.
intro /i a P x l/.
split.
  {
  intro /H/.
  inversion /H/.
  reintro /Hx Hl/.
  auto.
  }

  {
  intro /(Hx Hl)/.
  apply /Forall_cons/ >> auto.
  }
qed ();



lemma "Exists_cons_iff"
/
  forall i (a : U i) (P : a -> U i) x l .
    Exists P (x :: l)
    <->
    P x % Exists P l
/;

inference.
intro /i a P x l/.
split.
  {
  intro /H/.
  inversion /H/ >> auto.
  }

  {
  intro /(Hx | Hl)/.
    {
    apply /Exists_hit/ >> auto.
    }

    {
    apply /Exists_miss/ >> auto.
    }
  }
qed ();



lemma "Forall_append_iff"
/
  forall i (a : U i) (P : a -> U i) l1 l2 .
    Forall P (append l1 l2)
    <->
    Forall P l1 & Forall P l2
/;

inference.
intro /i a P l1 l2/.
induction /l1/.
  {
  rewrite /-> Forall_nil_iff/.
  rewrite /-> Logic.prod_id_l/.
  reflexivity.
  }

  {
  intro /h t IH/.
  rewrite /-> Forall_cons_iff at 0 0/.
  rewrite /-> IH/.
  rewrite /<- Logic.prod_assoc/.
  reflexivity.
  }
qed ();



lemma "Forall_append"
/
  forall i (a : U i) (P : a -> U i) l1 l2 .
    Forall P l1
    -> Forall P l2
    -> Forall P (append l1 l2)
/;

inference.
intro /i a P l1 l2 H1 H2/.
rewrite /-> Forall_append_iff/.
auto.
qed ();



lemma "Exists_append_iff"
/
  forall i (a : U i) (P : a -> U i) l1 l2 .
    Exists P (append l1 l2)
    <->
    Exists P l1 % Exists P l2
/;

inference.
intro /i a P l1 l2/.
induction /l1/.
  {
  rewrite /-> Exists_nil_iff/.
  rewrite /-> Logic.sum_id_l/.
  reflexivity.
  }

  {
  intro /h t IH/.
  rewrite /-> Exists_cons_iff at 0 0/.
  rewrite /-> IH/.
  rewrite /<- Logic.sum_assoc/.
  reflexivity.
  }
qed ();



lemma "Exists_append_1"
/
  forall i (a : U i) (P : a -> U i) l1 l2 .
    Exists P l1
    -> Exists P (append l1 l2)
/;

inference.
intro /i a P l1 l2 H/.
rewrite /-> Exists_append_iff/.
auto.
qed ();



lemma "Exists_append_2"
/
  forall i (a : U i) (P : a -> U i) l1 l2 .
    Exists P l2
    -> Exists P (append l1 l2)
/;

inference.
intro /i a P l1 l2 H/.
rewrite /-> Exists_append_iff/.
auto.
qed ();



lemma "In_append"
/
  forall i (a : U i) x l1 l2 .
    In a x (append l1 l2)
    <->
    In a x l1 % In a x l2
/;

inference.
intro /i a x l1 l2/.
rewrite /-> In_as_exists/.
rewrite /-> Exists_append_iff/.
rewrite /<- In_as_exists at all/.
reflexivity.
qed ();



lemma "Forall_implies"
/
  forall i (a : U i) (P Q : a -> U i) (l : list a) .
    (forall x . P x -> Q x)
    -> Forall P l
    -> Forall Q l
/;

inference.
intro /i a P Q l Himpl Hl/.
induction /Hl/ >> autoWith /Forall_nil/.
intro /h t Hh _ IH/.
apply /Forall_cons/ >> auto.
qed ();



lemma "Exists_implies"
/
  forall i (a : U i) (P Q : a -> U i) (l : list a) .
    (forall x . P x -> Q x)
    -> Exists P l
    -> Exists Q l
/;

inference.
intro /i a P Q l Himpl Hl/.
induction /Hl/ >> autoWith /Exists_miss/.
intro /h t Hh/.
apply /Exists_hit/ >> auto.
qed ();



lemma "Forall_map"
/
  forall i (a b : U i) (P : b -> U i) (f : a -> b) (l : list a) .
    Forall P (map f l)
    <->
    Forall (fn x . P (f x)) l
/;

inference.
intro /i a b P f l/.
rewrite /-> Forall_as_foldr at all/.
rewrite /-> foldr_map/.
apply /Logic.iff_refl/.
qed ();



lemma "Exists_map"
/
  forall i (a b : U i) (P : b -> U i) (f : a -> b) (l : list a) .
    Exists P (map f l)
    <->
    Exists (fn x . P (f x)) l
/;

inference.
intro /i a b P f l/.
rewrite /-> Exists_as_foldr at all/.
rewrite /-> foldr_map/.
apply /Logic.iff_refl/.
qed ();



lemma "In_map"
/
  forall i (a b : U i) (f : a -> b) (x : b) (l : list a) .
    In b x (map f l)
    <->
    (exists (y : a) .
       In a y l
       & x = f y : b)
/;

inference.
intro /i a b f x l/.
induction /l/.
  {
  split.
    {
    intro /0/.
    }

    {
    intro /(? 0 _)/.
    }
  }

  {
  intro /h t IH/.
  rewrite /-> IH/.
  split.
    {
    intro /(Heq | (y Hy Heq))/.
      {
      exists /h/.
      auto.
      }

      {
      exists /y/.
      auto.
      }
    }

    {
    intro /(y (Heqy | Hy) Heqx)/.
      {
      left.
      subst /y/.
      auto.
      }

      {
      right.
      exists /y/.
      auto.
      }
    }
  }
qed ();



lemma "Forall_reverse"
/
  forall i (a : U i) (P : a -> U i) l .
    Forall P (reverse l)
    <->
    Forall P l
/;

inference.
intro /i a P l/.
induction /l/ >> auto.
intro /h t IH/.
rewrite /-> Forall_cons_iff/.
rewrite /-> Forall_append_iff/.
rewrite /-> Forall_cons_iff/.
rewrite /-> Forall_nil_iff/.
rewrite /-> Logic.prod_id_r/.
rewrite /<- IH/.
rewrite /-> Logic.prod_commute/.
reflexivity.
qed ();



lemma "Exists_reverse"
/
  forall i (a : U i) (P : a -> U i) l .
    Exists P (reverse l)
    <->
    Exists P l
/;

inference.
intro /i a P l/.
induction /l/ >> auto.
intro /h t IH/.
rewrite /-> Exists_cons_iff/.
rewrite /-> Exists_append_iff/.
rewrite /-> Exists_cons_iff/.
rewrite /-> Exists_nil_iff/.
rewrite /-> Logic.sum_id_r/.
rewrite /<- IH/.
rewrite /-> Logic.sum_commute/.
reflexivity.
qed ();



lemma "In_reverse"
/
  forall i (a : U i) x l .
    In a x (reverse l)
    <->
    In a x l
/;

inference.
intro /i a x l/.
rewrite /-> In_as_exists at 0 0/.
apply /Exists_reverse/.
qed ();



lemma "decidable_Forall_dep"
/
  forall i (a : U i) (P : a -> U i) (l : list a) .
    (forall x . In a x l -> Decidable.decidable (P x))
    -> Decidable.decidable (Forall P l)
/;

inference.
intro /i a P l/.
induction /l/.

(* nil *)
{
intro /_/.
left.
apply /Forall_nil/.
}

(* cons *)
{
intro /h t IH Hdec/.
so /Hdec h (inl ())/ /Hh | Hno/.
1:{
  right.
  contrapositive /Hno/.
  inversion /Hno/.
  auto.
  }
so /IH __/ /Ht | Hno/.
  {
  intro /x Hx/.
  apply /Hdec/ >> auto.
  }

1:{
  right.
  contrapositive /Hno/.
  inversion /Hno/.
  auto.
  }
left.
apply /Forall_cons/ >> auto.
}
qed ();



lemma "decidable_Forall"
/
  forall i (a : U i) (P : a -> U i) .
    (forall x . Decidable.decidable (P x))
    -> forall (l : list a) .
         Decidable.decidable (Forall P l)
/;

inference.
intro /i a P Hdec l/.
apply /decidable_Forall_dep/ >> auto.
qed ();



lemma "decidable_Exists_dep"
/
  forall i (a : U i) (P : a -> U i) (l : list a) .
    (forall x . In a x l -> Decidable.decidable (P x))
    -> Decidable.decidable (Exists P l)
/;

inference.
intro /i a P l/.
induction /l/.

(* nil *)
{
intro /_/.
right.
intro /H/.
inversion /H/.
}

(* cons *)
{
intro /h t IH Hdec/.
so /Hdec h (inl ())/ /H | Hnoh/.
  {
  left.
  apply /Exists_hit/ >> auto.
  }
so /IH __/ /H | Hnot/.
  {
  intro /x Hx/.
  apply /Hdec/.
  right >> auto.
  }

  {
  left.
  apply /Exists_miss/ >> auto.
  }
right.
intro /H/.
inversion /H/.
  {
  reintro /Hh/.
  so /Hnoh Hh/ /0/.
  }

  {
  reintro /Ht/.
  so /Hnot Ht/ /0/.
  }
}
qed ();



lemma "decidable_Exists"
/
  forall i (a : U i) (P : a -> U i) .
    (forall x . Decidable.decidable (P x))
    -> forall (l : list a) .
         Decidable.decidable (Exists P l)
/;

inference.
intro /i a P Hdec l/.
apply /decidable_Exists_dep/ >> auto.
qed ();



lemma "decidable_In"
/
  forall i (a : U i) .
    (forall x y . Decidable.decidable (x = y : a))
    -> forall (x : a) (l : list a) .
         Decidable.decidable (In a x l)
/;

inference.
intro /i a Hdec x l/.
induction /l/.

(* nil *)
{
apply /Decidable.decidable_false/.
}

(* cons *)
{
intro /h t IH/.
so /Hdec x h/ /H | Hnoh/.
  {
  left.
  left.
  auto.
  }
so /IH/ /H | Hnot/.
  {
  left.
  right.
  auto.
  }
right.
intro /(Hh | Ht)/.
  {
  so /Hnoh Hh/ /0/.
  }

  {
  so /Hnot Ht/ /0/.
  }
}
qed ();



(* Nth *)

alias /option/ /Option.option/;
alias /None/ /Option.None/;
alias /Some/ /Option.Some/;


define /nth {a} l i/
/
  list_iter a (fn _ . nat -> option a)
    (fn _ . None)
    (fn h _ f i .
       nat_case i
         (Some h)
         (fn i' . f i'))
    l
    i
//
  intersect lv . forall (a : U lv) . list a -> nat -> option a
/;

inference.
unfold /nth/.
typecheck.
qed ();



reductions
/
  nth a (nil) _ --> None ;
  nth a (cons h t) i --> nat_case i (Some h) (fn i' . nth a t i') ;
  unfolding nth
/;



lemma "nth_within_length"
/
  forall lv (a : U lv) l i .
    i < length l
    <-> (exists x . nth l i = Some x : option a)
/;

inference.
intro /lv a l/.
induction /l/.

(* nil *)
{
intro /i/.
split.
  {
  intro /H/.
  so /Nat.lt_0_not _ H/ /0/.
  }

  {
  intro /(x H)/.
  injection /H/.
  }
}

(* cons *)
{
intro /h t IH i/.
destruct /i/ /| i/.
  {
  split.
    {
    intro /Hlt/.
    exists /h/.
    auto.
    }

    {
    intro /(x Heq)/.
    apply /Nat.lt_0_succ/.
    }
  }
split.
  {
  intro /Hlt/.
  apply /IH/.
  apply /Nat.lt_succ_invert/ >> auto.
  }

  {
  intro /(x Heq)/.
  apply /Nat.lt_succ_succ/.
  apply /IH/.
  exists /x/.
  reduce /Heq/.
  auto.
  }
}
qed ();



lemma "nth_outside_length"
/
  forall lv (a : U lv) l i .
    length l <= i
    <->
    nth l i = None : option a
/;

inference.
intro /lv a l/.
induction /l/.

(* nil *)
{
intro /i/.
split.
  {
  auto.
  }

  {
  intro /_/.
  apply /Nat.leq_0_min/.
  }
}

(* cons *)
{
intro /h t IH i/.
destruct /i/ /| i/.
  {
  split.
    {
    intro /Hleq/.
    so /Nat.leq_succ_0_not _ Hleq/ /0/.
    }

    {
    intro /Heq/.
    reduce /Heq/.
    injection /Heq/.
    }
  }
split.
  {
  intro /Hleq/.
  apply /IH/.
  apply /Nat.leq_succ_invert/ >> auto.
  }

  {
  intro /Heq/.
  apply /Nat.leq_succ_succ/.
  apply /IH/ >> auto.
  }
}
qed ();



lemma "nth_append_left"
/
  forall lv (a : U lv) l1 l2 i .
    i < length l1
    -> nth (append l1 l2) i = nth l1 i : option a
/;

inference.
intro /lv a l1 l2/.
induction /l1/.

(* nil *)
{
intro /i H/.
so /Nat.lt_0_not _ H/ /0/.
}

(* cons *)
{
intro /h t IH i Hlt/.
destruct /i/ /| i/.
  {
  reduce //.
  auto.
  }
reduce //.
apply /IH/.
apply /Nat.lt_succ_invert/ >> auto.
}
qed ();



lemma "nth_append_right"
/
  forall lv (a : U lv) l1 l2 i .
    length l1 <= i
    -> nth (append l1 l2) i = nth l2 (i - length l1) : option a
/;

inference.
intro /lv a l1 l2/.
induction /l1/.

(* nil *)
{
intro /i _/.
auto.
}

(* cons *)
{
intro /h t IH i Hleq/.
destruct /i/ /| i/.
  {
  so /Nat.leq_succ_0_not _ Hleq/ /0/.
  }
reduce //.
apply /IH/.
apply /Nat.leq_succ_invert/ >> auto.
}
qed ();



lemma "nth_map"
/
  forall lv (a b : U lv) (f : a -> b) l i .
    nth (map f l) i = Option.map f (nth l i) : option b
/;

inference.
intro /lv a b f l/.
induction /l/ >> auto.
intro /h t IH i/.
destruct /i/ /| i/.
  {
  reduce //.
  auto.
  }
reduce //.
apply /IH/.
qed ();



lemma "nth_In"
/
  forall lv (a : U lv) l i .
    Option.option_case (nth l i)
      unit
      (fn x . In a x l)
/;

inference.
intro /lv a l/.
induction /l/ >> auto.
intro /h t IH i/.
destruct /i/ /| i/.
  {
  reduce //.
  auto.
  }
reduce //.
so /IH i/ /H/.
revert /H/.
generalize /nth t i/ /_/ /x/.
destruct /x/ /{ | x }/ >> auto.
qed ();



lemma "list_eq_by_nth"
/
  forall i (a : U i) (l l' : list a) .
    length l = length l' : nat
    -> (forall j . j < length l -> nth l j = nth l' j : Option.option a)
    -> l = l' : list a
/;

inference.
intro /i a l/.
induction /l/.

(* nil *)
{
intro /ll Hlen _/.
destruct /ll/ /| ? ?/ >> auto.
reduce /Hlen/.
injection /Hlen/.
}

(* cons *)
{
intro /h t IH ll Hlen Hnth/.
destruct /ll/ /| h' t'/.
  {
  reduce /Hlen/.
  injection /Hlen/.
  }
compat.
  {
  so /Hnth 0 __ / /Heq/.
    {
    apply /Nat.lt_0_succ/.
    }
  reduce /Heq/.
  injection /Heq/.
  auto.
  }
apply /IH/.
  {
  reduce /Hlen/.
  apply /Nat.succ_inj/ >> auto.
  }
intro /j Hj/.
so /Hnth (succ j) __/ /H/.
  {
  apply /Nat.lt_succ_succ/ >> auto.
  }
reduce /H/.
hyp /H/.
}
qed ();



(* Zip *)

define /zip {a b} l1/
/
  list_iter a (fn _ . list b -> list (a & b))
    (fn _ . nil)
    (fn h1 _ f l2 .
       list_case l2
         nil
         (fn h2 t2 . (h1 , h2) :: f t2))
    l1
//
  intersect i . forall (a b : U i) . list a -> list b -> list (a & b)
/;

inference.
unfold /zip/.
typecheck.
qed ();



define /unzip {a b} l/
/
  list_iter (a & b) (fn _ . list a & list b)
    (nil , nil)
    (fn h _ p . (h #1 :: p #1 , h #2 :: p #2))
    l
//
  intersect i . forall (a b : U i) . list (a & b) -> list a & list b
/;

inference.
unfold /unzip/.
typecheck.
qed ();



reductions
/
  zip a b (nil) _ --> nil ;
  zip a b (cons h1 t1) l2 --> list_case b (list (a & b)) l2 nil (fn h2 t2 . cons (h1 , h2) (zip a b t1 t2)) ;

  unzip a b (nil) --> (nil , nil) ;
  unzip a b (cons h t) --> (cons (h #1) (unzip a b t #1) , cons (h #2) (unzip a b t #2)) ;
  
  unfolding zip unzip
/;



lemma "zip_unzip"
/
  forall i (a b : U i) (l : list (a & b)) .
    zip (unzip l #1) (unzip l #2) = l : list (a & b)
/;

inference.
intro /i a b l/.
induction /l/ >> auto.
(* cons *)
intro /h t IH/.
compat >> auto.
destruct /h/ /x y/.
reduce //.
reflexivity.
qed ();



lemma "unzip_zip"
/
  forall i (a b : U i) (l1 : list a) (l2 : list b) .
    length l1 = length l2 : nat
    -> unzip (zip l1 l2) = (l1 , l2) : (list a & list b)
/;

inference.
intro /i a b l1/.
induction /l1/.

(* nil *)
{
intro /l2 Hlen/.
destruct /l2/ /| h t/ >> auto.
reduce /Hlen/.
injection /Hlen/.
}

(* cons *)
{
intro /h1 t1 IH l2 Hlen/.
destruct /l2/ /| h2 t2/.
  {
  reduce /Hlen/.
  injection /Hlen/.
  }
reduce //.
reduce /Hlen/.
injection /Hlen/.
reintro /Hlen'/.
rewrite /-> IH at all/ >> auto.
}
qed ();



lemma "append_zip"
/
  forall i (a b : U i) (l1 l1' : list a) (l2 l2' : list b) .
    length l1 = length l2 : nat
    -> append (zip l1 l2) (zip l1' l2') = zip (append l1 l1') (append l2 l2') : list (a & b)
/;

inference.
intro /i a b l1/.
induction /l1/.

(* nil *)
{
intro /l1' l2 l2' Hlen/.
destruct /l2/ /| h2 t2/.
  {
  reduce //.
  reflexivity.
  }

  {
  reduce /Hlen/.
  injection /Hlen/.
  }
}

(* cons *)
{
intro /h1 t1 IH l1' l2 l2' Hlen/.
destruct /l2/ /| h2 t2/.
  {
  reduce /Hlen/.
  injection /Hlen/.
  }
reduce //.
reduce /Hlen/.
injection /Hlen/.
reintro /Hlen'/.
compat.
apply /IH/ >> auto.
}
qed ();



lemma "append_unzip"
/
  forall i (a b : U i) (l l' : list (a & b)) .
    append (unzip l #1) (unzip l' #1) = unzip (append l l') #1 : list a
    & append (unzip l #2) (unzip l' #2) = unzip (append l l') #2 : list b
/;

inference.
intro /i a b l l'/.
induction /l/.

(* nil *)
{
auto.
}

(* cons *)
{
intro /h t (IH1 IH2)/.
destruct /h/ /x y/.
reduce //.
split >> compat >> auto.
}
qed ();



lemma "length_zip"
/
  forall i (a b : U i) (l1 : list a) (l2 : list b) .
    length (zip l1 l2) = Nat.min (length l1) (length l2) : nat
/;

inference.
intro /i a b l1/.
induction /l1/.

(* nil *)
{
intro /l2/.
rewrite /-> Nat.min_ann_l/.
reflexivity.
}

(* cons *)
{
intro /h1 t1 IH1 l2/.
destruct /l2/ /| h2 t2/.
  {
  reduce //.
  rewrite /-> Nat.min_ann_r/.
  reflexivity.
  }
reduce //.
rewrite /-> Nat.min_succ/.
compat.
apply /IH1/.
}
qed ();



lemma "length_unzip"
/
  forall i (a b : U i) (l : list (a & b)) .
    length (unzip l #1) = length l : nat
    & length (unzip l #2) = length l : nat
/;

inference.
intro /i a b l/.
induction /l/ >> auto.
(* cons *)
intro /h t (IH1 IH2)/.
rewrite /-> IH1 , -> IH2/.
auto.
qed ();



lemma "reverse_zip"
/
  forall i (a b : U i) (l1 : list a) (l2 : list b) .
    length l1 = length l2 : nat
    -> reverse (zip l1 l2) = zip (reverse l1) (reverse l2) : list (a & b)
/;

inference.
intro /i a b l1/.
induction /l1/.

(* nil *)
{
intro /l2 Hlen/.
reflexivity.
}

(* cons *)
{
intro /h1 t1 IH l2 Hlen/.
destruct /l2/ /| h2 t2/.
  {
  reduce /Hlen/.
  injection /Hlen/.
  }
reduce /Hlen/.
injection /Hlen/.
reintro /Hlen'/.
reduce //.
rewrite /<- append_zip/.
  {
  rewrite /-> length_reverse at all/.
  hyp /Hlen'/.
  }
compat.
apply /IH/ >> auto.
}
qed ();



lemma "reverse_unzip"
/
  forall i (a b : U i) (l : list (a & b)) .
    reverse (unzip l #1) = unzip (reverse l) #1 : list a
    & reverse (unzip l #2) = unzip (reverse l) #2 : list b
/;

inference.
intro /i a b l/.
induction /l/ >> auto.
(* cons *)
intro /h t (IH1 IH2)/.
destruct /h/ /x y/.
reduce //.
rewrite /-> IH1 , -> IH2/.
rewrite /<- append_unzip _# 5 #1/.
rewrite /<- append_unzip _# 5 #2/.
auto.
qed ();



lemma "nth_zip"
/
  forall lv (a b : U lv) (l1 : list a) (l2 : list b) (i : nat) .
    nth (zip l1 l2) i
    =
    Option.bind
      (nth l1 i)
      (fn x .
         Option.bind
           (nth l2 i)
           (fn y . Option.Some (x , y)))
    : Option.option (a & b)
/;

inference.
intro /lv a b l1/.
induction /l1/ >> auto.
(* cons *)
intro /h1 t1 IH l2 i/.
destruct /l2/ /| h2 t2/.
  {
  reduce //.
  generalize /nat_case _ _ _/ /_/ /z/.
  destruct /z/ /{| z}/ >> reduce // >> reflexivity.
  }
reduce //.
destruct /i/ /| i/.
  {
  reduce //.
  reflexivity.
  }
reduce //.
rewrite /-> IH/.
reflexivity.
qed ();



lemma "nth_unzip"
/
  forall lv (a b : U lv) (l : list (a & b)) (i : nat) .
    nth (unzip l #1) i = Option.map (fn x . x #1) (nth l i) : Option.option a
    & nth (unzip l #2) i = Option.map (fn x . x #2) (nth l i) : Option.option b
/;

inference.
intro /lv a b l/.
induction /l/ >> auto.
(* cons *)
intro /h t IH i/.
destruct /i/ /| i/.
  {
  reduce //.
  auto.
  }
reduce //.
rewrite /-> IH i #1 , -> IH i #2/.
auto.
qed ();



(* Keep *)

defineInd /{a}/
/
  keep : nat -> list a -> list a of
  | zero . fn _ . nil

  | succ n .
      (fnind _ : list [a] -> list a of
       | nil . nil

       | cons h t . h :: keep n t)
//
  intersect i . forall (a : U i) . nat -> list a -> list a
/;



lemma "keep_nil"
/
  forall i (a : U i) n .
    keep n nil = nil : list a
/;

inference.
intro /i a n/.
destruct /n/ /| n/ >> auto.
qed ();



lemma "length_keep_min"
/
  forall i (a : U i) n (l : list a) .
    length (keep n l) = Nat.min n (length l) : nat
/;

inference.
intro /i a n/.
induction /n/.

(* 0 *)
{
intro /l/.
rewrite /-> Nat.min_ann_l/.
reflexivity.
}

(* succ *)
{
intro /n IH l/.
destruct /l/ /| h t/ >> reduce //.
  {
  rewrite /-> Nat.min_ann_r/.
  reflexivity.
  }

  {
  rewrite /-> Nat.min_succ/.
  compat.
  apply /IH/.
  }
}
qed ();



lemma "length_keep_leq"
/
  forall i (a : U i) n (l : list a) .
    length (keep n l) <= n
/;

inference.
intro /i a n l/.
rewrite /-> length_keep_min/.
apply /Nat.min_leq_l/.
qed ();



lemma "length_keep"
/
  forall i (a : U i) n (l : list a) .
    n <= length l
    -> length (keep n l) = n : nat
/;

intro /i a n l Hlen/.
rewrite /-> length_keep_min/.
rewrite /-> Nat.min_eq_l/ >> auto.
qed ();



lemma "keep_idem"
/
  forall i (a : U i) n (l : list a) .
    keep n (keep n l) = keep n l : list a
/;

inference.
intro /i a n/.
induction /n/.

(* 0 *)
{
auto.
}

(* succ *)
{
intro /n IH (| h t)/ >> reduce // >> auto.
}
qed ();



lemma "keep_append_leq"
/
  forall i (a : U i) n (l1 l2 : list a) .
    n <= length l1
    -> keep n (append l1 l2) = keep n l1 : list a
/;

inference.
intro /i a n/.
induction /n/.

(* 0 *)
{
auto.
}

(* succ *)
{
intro /n IH (| h l1) l2 Hleq/.
  {
  reduce /Hleq/.
  so /Nat.leq_succ_0_not _ Hleq/ /0/.
  }

  {
  reduce //.
  compat.
  apply /IH/.
  apply /Nat.leq_succ_invert/.
  auto.
  }
}
qed ();



lemma "keep_append_geq"
/
  forall i (a : U i) n (l1 l2 : list a) .
    length l1 <= n
    -> keep n (append l1 l2) = append l1 (keep (n - length l1) l2) : list a
/;

inference.
intro /i a n/.
induction /n/.

(* 0 *)
{
intro /l1 l2 Hlen/.
destruct /l1/ /| h t/.
1:{
  reduce /Hlen/.
  so /Nat.leq_succ_0_not _ Hlen/ /0/.
  }
reduce //.
auto.
}

(* succ *)
{
intro /n IH (| h l1) l2 Hleq/.
  {
  reduce //.
  reflexivity.
  }
reduce //.
compat.
apply /IH/.
apply /Nat.leq_succ_invert/.
auto.
}
qed ();



lemma "keep_append_eq"
/
  forall i (a : U i) n (l1 l2 : list a) .
    n = length l1 : nat
    -> keep n (append l1 l2) = l1 : list a
/;

inference.
intro /i a n l1 l2 Heq/.
subst /n/.
rewrite /-> keep_append_geq/.
rewrite /-> Nat.minus_self/.
reduce //.
rewrite /-> append_id_r/.
reflexivity.
qed ();



lemma "keep_all"
/
  forall i (a : U i) (l : list a) .
    keep (length l) l = l : list a
/;

inference.
intro /i a l/.
rewrite /<- append_id_r _ _ l at 1/.
apply /keep_append_eq/.
reflexivity.
qed ();



lemma "keep_map"
/
  forall i (a b : U i) n (f : a -> b) (l : list a) .
    keep n (map f l) = map f (keep n l) : list b
/;

inference.
intro /i a b n f/.
induction /n/ >> auto.
(* succ *)
intro /n IH l/.
destruct /l/ /| x l/ >> reduce // >> auto.
qed ();



lemma "Forall_keep_weaken"
/
  forall i (a : U i) (P : a -> U i) n (l : list a) .
    Forall P l
    -> Forall P (keep n l)
/;

inference.
intro /i a P n/.
induction /n/ >> reduce //.

(* 0 *)
{
intro /l _/.
apply /Forall_nil/.
}

(* succ *)
{
intro /n IH l Hall/.
destruct /l/ /| x l/ >> reduce // >> auto.
inversion /Hall/.
reintro /Hx Hl/.
apply /Forall_cons/ >> auto.
}
qed ();



lemma "Exists_keep_weaken"
/
  forall i (a : U i) (P : a -> U i) n (l : list a) .
    Exists P (keep n l)
    -> Exists P l
/;

inference.
intro /i a P n/.
induction /n/ >> reduce //.

(* 0 *)
{
intro /l Hex/.
inversion /Hex/.
}

(* succ *)
{
intro /n IH l Hex/.
destruct /l/ /| x l/ >> reduce // >> auto.
reduce /Hex/.
inversion /Hex/ >> autoWith /Exists/.
}
qed ();



lemma "In_keep_weaken"
/
  forall i (a : U i) x n (l : list a) .
    In a x (keep n l)
    -> In a x l
/;

inference.
intro /i a x n/.
induction /n/.

(* 0 *)
{
intro /l 0/.
}

(* succ *)
{
intro /n IH (| h t) Hin/ >> reduce /Hin/.
  {
  so /Hin/ /0/.
  }

  {
  reduce //.
  destruct /Hin/ /? | ?/ >> auto.
  }
}
qed ();



lemma "nth_keep"
/
  forall i (a : U i) m n (l : list a) .
    n < m
    -> nth (keep m l) n = nth l n : option a
/;

inference.
intro /i a m/.
induction /m/.

(* 0 *)
{
intro /n l Hlt/.
so /Nat.lt_0_not _ Hlt/ /0/.
}

(* succ *)
{
intro /m IH n l Hlt/.
destruct /n/ /| n/ >> destruct /l/ /| h t/ >> reduce // >> auto.
apply /IH/.
apply /Nat.lt_succ_invert/ >> auto.
}
qed ();



(* Drop *)

defineInd
/
  {a}
//
  drop : nat -> list a -> list a of
  | zero . fn l . l

  | succ n . fn l .
      list_case l nil (fn _ l' . drop n l')
//
  intersect i . forall (a : U i) . nat -> list a -> list a
/;



lemma "drop_nil"
/
  forall i (a : U i) n .
    drop n nil = nil : list a
/;

inference.
intro /i a n/.
destruct /n/ /| n/ >> auto.
qed ();



lemma "length_drop"
/
  forall i (a : U i) n (l : list a) .
    length (drop n l) = length l - n : nat
/;

inference.
intro /i a n/.
induction /n/ >> auto.
(* succ *)
intro /n IH l/.
destruct /l/ /| x l/ >> reduce // >> autoWith /Nat.minus_0_l/.
qed ();



lemma "drop_plus"
/
  forall i (a : U i) (l : list a) m n .
    drop n (drop m l) = drop (m + n) l : list a
/;

inference.
intro /i a l m n/.
revert /l/.
induction /m/ >> auto.
(* succ *)
intro /m IH l/.
destruct /l/ /| h t/.
  {
  reduce //.
  apply /drop_nil/.
  }
reduce //.
apply /IH/.
qed ();



lemma "drop_append_leq"
/
  forall i (a : U i) n (l1 l2 : list a) .
    n <= length l1
    -> drop n (append l1 l2) = append (drop n l1) l2 : list a
/;

inference.
intro /i a n l1 l2/.
revert /l1/.
induction /n/ >> auto.
(* succ *)
intro /n IH l1 Hleq/.
destruct /l1/ /| x l1/.
  {
  reduce /Hleq/.
  so /Nat.leq_succ_0_not _ Hleq/ /0/.
  }
reduce //.
apply /IH/.
reduce /Hleq/.
apply /Nat.leq_succ_invert/ >> auto.
qed ();



lemma "drop_append_geq"
/
  forall i (a : U i) n (l1 l2 : list a) .
    length l1 <= n
    -> drop n (append l1 l2) = drop (n - length l1) l2 : list a
/;

inference.
intro /i a n l1 l2/.
revert /n/.
induction /l1/ >> auto.
(* cons *)
intro /x l1 IH n Hleq/.
destruct /n/ /| n/.
  {
  so /Nat.leq_succ_0_not _ Hleq/ /0/.
  }
reduce //.
apply /IH/.
apply /Nat.leq_succ_invert/ >> auto.
qed ();



lemma "drop_append_eq"
/
  forall i (a : U i) n (l1 l2 : list a) .
    n = length l1 : nat
    -> drop n (append l1 l2) = l2 : list a
/;

inference.
intro /i a n l1 l2 Heq/.
subst /n/.
rewrite /-> drop_append_geq/.
rewrite /-> Nat.minus_self/.
reflexivity.
qed ();



lemma "drop_all"
/
  forall i (a : U i) (l : list a) .
    drop (length l) l = nil : list a
/;

inference.
intro /i a l/.
rewrite /<- append_id_r _ _ l at 1/.
apply /drop_append_eq/.
reflexivity.
qed ();



lemma "drop_map"
/
  forall i (a b : U i) n (f : a -> b) (l : list a) .
    drop n (map f l) = map f (drop n l) : list b
/;

inference.
intro /i a b n f/.
induction /n/ >> auto.
(* succ *)
intro /n IH l/.
destruct /l/ /| x l/ >> reduce // >> auto.
qed ();



lemma "Forall_drop_weaken"
/
  forall i (a : U i) (P : a -> U i) n (l : list a) .
    Forall P l
    -> Forall P (drop n l)
/;

inference.
intro /i a P n/.
induction /n/ >> reduce // >> auto.
(* succ *)
intro /n IH l Hall/.
destruct /l/ /| x l/ >> reduce // >> auto.
apply /IH/.
inversion /Hall/ >> auto.
qed ();



lemma "Exists_drop_weaken"
/
  forall i (a : U i) (P : a -> U i) n (l : list a) .
    Exists P (drop n l)
    -> Exists P l
/;

inference.
intro /i a P n/.
induction /n/ >> reduce // >> auto.
(* succ *)
intro /n IH l Hex/.
destruct /l/ /| x l/ >> reduce // >> auto.
reduce /Hex/.
apply /Exists_miss/ >> auto.
qed ();



lemma "In_drop_weaken"
/
  forall i (a : U i) x n (l : list a) .
    In a x (drop n l)
    -> In a x l
/;

inference.
intro /i a x n/.
induction /n/ >> reduce // >> auto.
intro /n IH l Hin/.
destruct /l/ /| y l/ >> reduce /Hin/.
  {
  destruct /Hin/ /0/.
  }
reduce //.
right.
apply /IH/ >> auto.
qed ();



lemma "nth_drop"
/
  forall i (a : U i) m n (l : list a) .
    nth (drop m l) n = nth l (m + n) : option a
/;

inference.
intro /i a m/.
induction /m/ >> auto.
(* succ *)
intro /m IH n l/.
destruct /l/ /| x l/ >> reduce // >> auto.
qed ();



lemma "nth_as_drop"
/
  forall i (a : U i) (l : list a) n .
    nth l n = list_case (drop n l) None (fn h _ . Some h) : option a
/;

inference.
intro /i a l n/.
revert /n/.
induction /l/.

(* nil *)
{
intro /n/.
rewrite /-> drop_nil/.
reduce //.
reflexivity.
}

(* cons *)
{
intro /x l IH n/.
destruct /n/ /| n/ >> reduce // >> auto.
}
qed ();



lemma "split_list"
/
  forall i (a : U i) n (l : list a) .
    l = append (keep n l) (drop n l) : list a
/;

inference.
intro /i a n/.
induction /n/ >> auto.
(* succ *)
intro /n IH l/.
destruct /l/ /| h t/ >> auto.
reduce //.
compat.
apply /IH/.
qed ();

  

(* Map_flat *)

defineInd /{a b} f/
/
  map_flat : list [a] -> list b of
  | nil . nil
  | cons h t . append (f h) (map_flat t)
//
  intersect i .
  forall (a b : U i) . (a -> list b) -> list a -> list b
/;
  


lemma "In_map_flat"
/
  forall i (a b : U i) (f : a -> list b) (x : b) (l : list a) .
    In b x (map_flat f l) <-> (exists y . In a y l & In b x (f y))
/;

inference.
intro /i a b f x l/.
induction /l/.

(* nil *)
{
split.
  {
  intro /0/.
  }

  {
  intro /(y 0 _)/.
  }
}

(* cons *)
{
intro /h t IH/.
split.
  {
  intro /Hin/.
  rewrite /-> In_append in Hin/.
  destruct /Hin/ /Hin | Hin/.
    {
    exists /h/ >> auto.
    }

    {
    so /IH #1 Hin/ /y Hiny Hinx/.
    exists /y/.
    split >> auto.
    }
  }

  {
  intro /(y (Heq | Hiny) Hinx)/.
    {
    subst /y/.
    rewrite /-> In_append/.
    left >> auto.
    }

    {
    rewrite /-> In_append/.
    right.
    apply /IH/.
    existsAuto.
    }
  }
}
qed ();
 


lemma "map_flat_as_foldr"
/
  forall i (a b : U i) (f : a -> list b) (l : list a) .
    map_flat f l = foldr nil (fn h t . append (f h) t) l : list b
/;

inference.
intro /i a b f l/.
induction /l/ >> auto.
qed ();



(* For all pairs of distinct elements *)

typedef
/
  datatype
    intersect (i : level) .
    forall (a : U i) (P : a -> a -> U i) .
    U i
  of
    Forall_dist : list a -> type =

    | Forall_dist_nil :
        Forall_dist nil

    | Forall_dist_cons :
        forall h t .
          Forall (P h) t
          -> Forall_dist t
          -> Forall_dist (cons h t)
/;

Database.setImplicits (parseConstant /Forall_dist/) 1;
Database.setImplicits (parseConstant /Forall_dist_nil/) 1;
Database.setImplicits (parseConstant /Forall_dist_cons/) 1;



lemma "Forall_dist_nil_iff"
/
  forall i (a : U i) (P : a -> a -> U i) .
    Forall_dist P nil
    <->
    unit
/;

inference.
intro /i a P/.
split >> auto.
intro /_/.
apply /Forall_dist_nil/.
qed ();



lemma "Forall_dist_cons_iff"
/
  forall i (a : U i) (P : a -> a -> U i) x l .
    Forall_dist P (x :: l)
    <->
    Forall (P x) l & Forall_dist P l
/;

inference.
intro /i a P x l/.
split.
  {
  intro /H/.
  inversion /H/.
  auto.
  }

  {
  intro /(Hx Hl)/.
  apply /Forall_dist_cons/ >> auto.
  }
qed ();



lemma "Forall_dist_append_iff"
/
  forall i (a : U i) (P : a -> a -> U i) l1 l2 .
    Forall_dist P (append l1 l2)
    <->
    Forall (fn x . Forall (P x) l2) l1 & Forall_dist P l1 & Forall_dist P l2
/;

inference.
intro /i a P l1 l2/.
split.
  {
  revert /l2/.
  induction /l1/.
    (* nil *)
    {
    intro /l2 H/.
    splitn 2 >> autoWith /Forall_nil Forall_dist_nil/.
    }

    (* cons *)
    {
    intro /h t IH l2 Hall/.
    inversion /Hall/.
    reintro /Hh_t_l2 Hall'/.
    so /IH _ Hall'/ /Ht_l2 Ht Hl2/.
    rewrite /-> Forall_append_iff in Hh_t_l2/.
    destruct /Hh_t_l2/ /Hh_t Hh_l2/.
    splitn 2 >> autoWith /Forall_cons Forall_dist_cons/.
    }
  }

  {
  revert /l2/.
  induction /l1/.
    (* nil *)
    {
    intro /l2 (_ _ H)/.
    auto.
    }

    (* cons *)
    {
    intro /h t IH l2 (Hht_l2 Hht Hl2)/.
    inversion /Hht_l2/.
    reintro /Hh_l2 Ht_l2/.
    inversion /Hht/.
    reintro /Hh_t Ht/.
    apply /Forall_dist_cons/ >> auto.
    apply /Forall_append/ >> auto.
    }
  }
qed ();



lemma "Forall_dist_append"
/
  forall i (a : U i) (P : a -> a -> U i) l1 l2 .
    Forall (fn x . Forall (P x) l2) l1
    -> Forall_dist P l1
    -> Forall_dist P l2
    -> Forall_dist P (append l1 l2)
/;

inference.
intro /i a P l1 l2 Hl1_l2 Hl1 Hl2/.
apply /Forall_dist_append_iff/ >> auto >> done.
qed ();



lemma "Forall_dist_implies"
/
  forall i (a : U i) (P Q : a -> a -> U i) (l : list a) .
    (forall x y . P x y -> Q x y)
    -> Forall_dist P l
    -> Forall_dist Q l
/;

inference.
intro /i a P Q l Himpl Hall/.
induction /Hall/ >> autoWith /Forall_dist_nil/.
(* cons *)
intro /h t Hh _ IH/.
apply /Forall_dist_cons/ >> auto.
apply /Forall_implies/ >> auto.
qed ();



lemma "Forall_dist_map"
/
  forall i (a b : U i) (P : b -> b -> U i) (f : a -> b) (l : list a) .
    Forall_dist P (map f l)
    <->
    Forall_dist (fn x y . P (f x) (f y)) l
/;

inference.
intro /i a b P f l/.
split.
  {
  induction /l/.
    {
    intro /_/.
    apply /Forall_dist_nil/.
    }

    {
    intro /h t IH Hall/.
    inversion /Hall/.
    reintro /Hh Ht/.
    apply /Forall_dist_cons/ >> auto.
    reduce //.
    rewrite /-> Forall_map in Hh/ >> auto.
    }
  }

  {
  induction /l/.
    {
    intro /_/.
    apply /Forall_dist_nil/.
    }

    {
    intro /h t IH Hall/.
    inversion /Hall/.
    reintro /Hh Ht/.
    apply /Forall_dist_cons/ >> autoWith /Forall_map/.
    }
  }
qed ();



lemma "Forall_dist_reverse"
/
  forall i (a : U i) (P : a -> a -> U i) (l : list a) .
    (forall x y . P x y -> P y x)
    -> Forall_dist P (reverse l)
       <->
       Forall_dist P l
/;

inference.
intro /i a P l Hsymm/.
induction /l/ >> auto.
intro /h t IH/.
rewrite /-> Forall_dist_cons_iff/.
rewrite /-> Forall_dist_append_iff/.
rewrite /-> Forall_dist_cons_iff/.
rewrite /-> Forall_nil_iff/.
rewrite /-> Forall_dist_nil_iff/.
rewrite /-> Logic.prod_id_r at 0 0/.
rewrite /-> IH/.
rewrite /-> Forall_reverse/.
rewrite /-> Forall_forall at 0 0/.
reduce //.
rewrite /-> Forall_cons_iff within x/.
rewrite /-> Forall_nil_iff within x/.
rewrite /-> Logic.prod_id_r within x/.
assert /forall x . P x h <-> P h x/ /H/.
  {
  inference.
  intro /x/.
  split >> auto.
  }
rewrite /-> H within x/.
reflexivity.
qed ();



lemma "decidable_Forall_dist_dep"
/
  forall i (a : U i) (P : a -> a -> U i) (l : list a) .
    (forall x y . In a x l -> In a y l -> Decidable.decidable (P x y))
    -> Decidable.decidable (Forall_dist P l)
/;

inference.
intro /i a P l/.
induction /l/.

(* nil *)
{
intro /_/.
left.
apply /Forall_dist_nil/.
}

(* cons *)
{
intro /h t IH Hdec/.
rewrite /-> Forall_dist_cons_iff/.
so /IH __/ /Ht | Hno/.
  {
  intro /x y Hx Hy/.
  apply /Hdec/ >> right >> auto.
  }

1:{
  right.
  contrapositive /Hno/.
  apply /Hno/.
  }
so /decidable_Forall_dep _ _ (P h) t __/ /Hh | Hno/.
  {
  intro /x Hx/.
  apply /Hdec/ >>> [left, right] >> auto.
  }

1:{
  right.
  contrapositive /Hno/.
  apply /Hno/.
  }
left.
auto.
}
qed ();



lemma "decidable_Forall_dist"
/
  forall i (a : U i) (P : a -> a -> U i) (l : list a) .
    (forall x y . Decidable.decidable (P x y))
    -> Decidable.decidable (Forall_dist P l)
/;

inference.
intro /i a P l Hdec/.
apply /decidable_Forall_dist_dep/ >> auto.
qed ();  



(* Covariance *)

lemma "list_subtype"
/
  forall i (a b : U i) . a <: b -> list a <: list b
/;

inference.
intro /i a b Hsub l/.
sinduction /l/.
reintro /IH/.
destruct /l/ /| h t/ >> auto.
fold /cons ap i/.
typecheck1 >> auto.
apply /IH/.
apply /cons_subterm/.
qed ();

val list_subtype = parseConstant /list_subtype/;



writeRegistry /AfterListAllEnv/
  /[constant constant constant constant constant constant constant]/
  (Collapse (list, list_uptype, cForall, cForall_uptype, cExists, cExists_uptype, list_subtype));


endModule ();
