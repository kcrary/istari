
signature AUTO_REDUCE =
   sig

      type constant = Constant.constant
      type term = Term.term
      type reduction = Reduction.reduction


      (* autoreduce underlam dp dl m

         Reduces m to normal form, where constants in dp can be reduced unlimited times,
         and constants in dl can be reduced at most once and only at the root.  Reduces
         under lambdas if underlam.

         Returns the normal form n, and a reduction from m to n.
      *)
      val autoreduce :
         bool
         -> (constant * reduction list) list
         -> (constant * reduction) list
         -> term
         -> term * reduction


      structure Dict : DICT where type key = constant

      val autoreduce' : 
         bool
         -> reduction list Dict.dict
         -> reduction Dict.dict
         -> term
         -> term * reduction
      

      (* new underlam dp dl const zones n P

         underlam, dp, dl as in autoreduce
         const, zones, n, P as in Reduction.new

         dl is available only to the left-hand-reduction
      *)
      val new :
         bool
         -> (constant * reduction list) list
         -> (constant * reduction) list
         -> constant -> (int * constant * int) list -> int -> term -> Reduction.ureduction

   end


structure AutoReduce :> AUTO_REDUCE =
   struct

      type constant = Constant.constant
      type term = Term.term
      type reduction = Reduction.reduction

      structure N = Normalize
      structure R = Reduction
      structure T = Term

      structure Dict = RedBlackDict (structure Key = Constant.Ordered)
      structure D = Dict


      (* This could be made more efficient, but it seems fine for now. *)

      fun reductionNorm underlam dp dl m acc =
         (case N.simplify m of
             T.Elim (T.Const const, _) =>
                (case 
                    List.findmap 
                       (fn r =>
                           (try
                               SOME (r, R.reduce r m)
                            with R.Reduce => NONE))
                       (case D.find dp const of
                           NONE => []
 
                         | SOME l => l)
                 of
                    NONE =>
                       (case
                           (case D.find dl const of
                               NONE => NONE

                             | SOME r =>
                                  (try
                                      SOME (r, R.reduce r m)
                                   with R.Reduce => NONE))
                        of
                           NONE =>
                              (m, R.trans (List.rev acc))

                         | SOME (r, m') =>
                              reduction underlam dp (D.remove dl const) m' (r :: acc))

                  | SOME (r, m') =>
                       reduction underlam dp dl m' (r :: acc))

           | _ =>
                (m, R.trans (List.rev acc)))


      and reduction underlam dp dl m acc =
         let
            val m' = N.simplify (R.reduce R.betas m)
         in
            (case m' of
                T.Elim (h, spine) =>
                   let
                      val dl' =
                         (case h of
                             T.Const const => D.remove dl const
                           | _ => dl)

                      val (spine', rs) =
                         List.foldr
                            (fns elim (spine', rs) =>
                                (case elim of
                                    T.App n =>
                                       let
                                          val (n', r) = reduction underlam dp dl' n []
                                       in
                                          (T.App n' :: spine', r :: rs)
                                       end

                                  | _ =>
                                       (elim :: spine', rs)))
                            ([], [])
                            spine
                   in
                      reductionNorm underlam dp dl (T.Elim (h, spine')) (R.compat rs :: R.betas :: acc)
                   end

              | T.Lam (binder, m1) =>
                   if underlam then
                      let
                         val (m1', r1) = reduction underlam dp dl m1 []
                      in
                         (T.Lam (binder, m1'),
                          R.trans (List.rev (R.within [0] r1 :: R.betas :: acc)))
                      end
                   else
                      (m', R.trans (List.rev (R.betas :: acc)))

              | T.Pair (m1, m2) =>
                   let
                      val (m1', r1) = reduction underlam dp dl m1 []
                      val (m2', r2) = reduction underlam dp dl m2 []
                   in
                      (T.Pair (m1', m2'),
                       R.trans (List.rev (R.within [1] r2 :: R.within [0] r1 :: R.betas :: acc)))
                   end

              | T.Next m1 =>
                   let
                      val (m1', r1) = reduction underlam dp dl m1 []
                   in
                      (T.Next m1',
                       R.trans (List.rev (R.within [0] r1 :: R.betas :: acc)))
                   end

              | _ => 
                   (m', R.trans (List.rev (R.betas :: acc))))
         end


      fun autoreduce underlam dp dl m =
         let
            val dp' =
               List.foldl
                  (fns (const, l) dp' => D.insert dp' const l)
                  D.empty
                  dp

            val dl' =
               List.foldl
                  (fns (const, r) dl' => D.insert dl' const r)
                  D.empty
                  dl
         in
            reduction underlam dp' dl' m [] 
         end

      fun autoreduce' underlam dp dl m = reduction underlam dp dl m []

      

      fun new underlam dp dl const zones n p =
         if
            n >= 0 andalso List.all (fn (l, _, m) => l >= 0 andalso n >= 0) zones
         then
            let
               val (_, spine) =
                  List.foldr
                     (fns (l, const2, m) (i, spine) =>
                         let
                            val innerspine =
                               Int.natrecUp (fns j sp => T.App (T.Var (i+j)) :: sp) [] m

                            val spine' = T.App (T.Elim (T.Const const2, innerspine)) :: spine
                            
                            val spine'' =
                               Int.natrecUp (fns j sp => T.App (T.Var (i+m+j)) :: sp) spine' l
                         in
                            (i + l + m, spine'')
                         end)
                     (n, Int.natrecUp (fns j sp => T.App (T.Var j) :: sp) [] n)
                     zones

               val before = T.Elim (T.Const const, spine)

               val dp' =
                  List.foldl
                     (fns (const, l) dp' => D.insert dp' const l)
                     D.empty
                     dp
   
               val dl' =
                  List.foldl
                     (fns (const, r) dl' => D.insert dl' const r)
                     D.empty
                     dl

               val (_, red1) = reduction underlam dp' dl' before []
               val (_, red2) = reduction underlam dp' D.empty p []
            in
               R.new const zones n p red1 red2
            end
         else
            raise R.Reduce

   end
