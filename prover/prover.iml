
signature PROVER =
   sig

      exception Prover of string

      datatype problem = NotGround | Open | Marker
      exception Unclean of problem * Term.term

      val apply : Tactic.tactic -> unit
      val enter : unit -> unit
      val leave : unit -> unit

      val reorder : Message.label Reorder.reordering -> unit
      val pull : int -> unit
      val push : int -> unit

      val entern : int -> unit

      val lemma : string -> ETerm.eterm -> unit
      val abandon : unit -> unit
      val qed : unit -> unit

      val typechecker : unit -> unit

      val show : unit -> unit
      val showFull : unit -> unit
      val showLiteral : unit -> unit
      val alwaysShowFull : bool ref

      val goals : unit -> Tactic.goal list
      val currentGoal : unit -> Tactic.goal
      val currentConcl : unit -> Term.term
      val idir : unit -> Directory.idirectory
      val parseCurr : ETerm.eterm -> Term.term
      val showCurr : Term.term -> unit

      val detail : unit -> unit

      val openGoal : Tactic.goal -> unit

      val originalGoals : unit -> Tactic.goal list
      val showOriginalGoals : unit -> unit

      val underway : unit -> bool

   end


signature PROVER_INTERNAL =
   sig

      include PROVER


      (* returns NONE if the term is ground, closed, and contains no markers *)
      val checkClean : Term.term -> unit


      val setGoals : (Message.label * Tactic.goal) list -> (Refine.validation list -> unit) -> unit


      (* sets a persistent checkpoint if not within a lemma *)
      val checkpoint : unit -> unit

      (* called before lemmas *)
      val beforeLemmaHook : (unit -> unit) ref

   end


structure ProverInternal :> PROVER_INTERNAL =
   struct

      structure R = CheckpointedRef
      structure J = Judgement
      structure T = Term

      type validation = Refine.validation
      type goal = Tactic.goal
      type label = Message.label

      val blank = Message.blank

      exception Prover of string

      fun signalError msg = raise (Prover
                                      (String.concat
                                          [
                                          "Error: ",
                                          msg,
                                          ".\n"
                                          ]))



      val () = 
         Handler.customHandler
         (fn Prover msg => 
                (
                print msg; 
                true
                )

           | _ => false)



      (* The state of a proof underway is (d, juds, k)
         where:
         d is the current depth
         juds are the current goals
         k is a continuation expecting validations for all the current goals
            and updating the state accordingly

         Note that k expects a list of the same length as juds.
         (The unit in the type of goals is to match up with the tactic type.)
      *)

      type state = int * (label * goal) list * (Refine.validation list -> unit)

      val theState : state option R.cref = R.new NONE

      fun state () =
         (case R.deref theState of
             NONE =>
                signalError "no proof underway"

           | SOME st => st)



      val alwaysShowFull = ref false

      fun showGoal shower i label goal =
         (
         print "[goal ";
         print (Int.toString i);
         print "]\n";
         Message.printLabel label;
         
         shower goal;
         print "\n"
         )

      fun showGoalsLoop shower i goals =
         (case goals of
             [] => ()

           | (label, goal) :: rest =>
                (
                showGoalsLoop shower (i+1) rest;
                showGoal shower i label goal
                ))

      fun showGoalsGen depth goals showfirst showrest =
         (
         print "========================================\n";
         (case goals of
             [] =>
                (
                print "no goals (depth ";
                print (Int.toString depth);
                print ")\n"
                )

           | [(label, goal)] =>
                (
                Message.printLabel label;
                showfirst goal;
                print "\n1 goal (depth ";
                print (Int.toString depth);
                print ")\n"
                )

           | (label, goal) :: rest =>
                (
                showGoalsLoop showrest 1 rest;
                showGoal showfirst 0 label goal;

                print (Int.toString (List.length goals));
                print " goals (depth ";
                print (Int.toString depth);
                print ")\n"
                ));
         print "----------------------------------------\n"
         )

      fun showGoalsMain showfirst showrest =
         (case R.deref theState of
             SOME (depth, goals, _) =>
                showGoalsGen depth goals showfirst showrest

           | NONE => ())

      fun showerShort (jud, dir) = Show.show dir (J.concl jud)

      fun showerLong goal = Show.showGoal goal

      fun showerLiteral (jud, _) = Show.showLiteralGoal jud;

      fun showGoals () =
         if !alwaysShowFull then
            showGoalsMain showerLong showerLong
         else
            showGoalsMain showerLong showerShort

      val show = showGoals

      fun showFull () = showGoalsMain showerLong showerLong

      fun showLiteral () = showGoalsMain showerLiteral showerLiteral

      fun detail () =
         (case R.deref theState of
             NONE => signalError "no proof underway"

           | SOME (_, [], _) => raise (Fail "no current goal")
         
           | SOME (_, (label, jud) :: _, _) => Message.detail label)




      fun action f =
         (
         Message.clear ();

         f (state ());

         Message.message 10 0 showGoals
         )


      extension Answer of (label * goal) list * Tactic.validator

      fun apply tac =
         action
         (fn (_, [], _) => signalError "no current goal"

           | (d, [(_, goal)], k) =>
                (case
                    tac goal
                       (fn msg => signalError msg)
                       (fn (subgoals, validate, _) =>
                           Answer (subgoals, validate))
                 of
                    Answer (subgoals, validate) =>
                       R.assign theState
                       (SOME
                           (d, subgoals,
                            (fn vs => 
                                (case validate vs of
                                    (v, []) => k [v]
 
                                  | _ => raise Refine.ValidationFailure))))

                  | _ => raise (Fail "impossible"))

           | (_, _ :: _, _) => signalError "multiple current goals")


      fun enter () =
         action
         (fn (_, [], _) => signalError "no current goals"

           | (d, goal :: rest, k) =>
                let
                   fun k' vs =
                      (case vs of
                          [v] =>
                             R.assign theState
                             (SOME (d, rest, (fn vs' => k (v :: vs'))))

                        | _ => raise (Fail "convention violated"))
                in
                   R.assign theState
                   (SOME (d+1, [goal], k'))
                end)


      fun leave () =
         action
         (fn (d, [], k) =>
                if d = 0 then
                   (* To end the proof, we want to use qed instead of leave. *)
                   signalError "top frame"
                else
                   k []

           | _ => signalError "current goals exist")


      fun reorder reord =
         action
         (fn (d, goals, k) =>
             (try
                 let
                    val (goals', perm) = reord goals
                 in
                    R.assign theState
                    (SOME (d, goals', (fn vs => k (perm vs))))
                 end
              with
                 Reorder.Reorder msg => signalError msg))
                
      fun pull i = reorder (Reorder.pull i)
      fun push i = reorder (Reorder.push i)


      fun entern i =
         (
         pull i;
         enter ()
         )
                      

      fun qed () =
         (
         Message.clear ();

         (case state () of
             (0, [], k) => k []

           | _ => signalError "proof incomplete")
         )


      fun abandon () =
         (
         Message.clear ();

         (case R.deref theState of
             SOME _ =>
                R.assign theState NONE

           | NONE =>
                signalError "no proof underway")
         )


         
      val beforeLemmaHook : (unit -> unit) ref = ref (fn () => ())

      val theOriginalGoals : (label * goal) list option R.cref = R.new NONE


      datatype problem = NotGround | Open | Marker
      exception Unclean of problem * T.term
      exception UncleanInternal of problem


      (* checks that m[s] is ground, its highest free variable is < k, and contains no markers *)
      fun cleanLoop k m s =
         (case m of
             T.Var i =>
                (case T.substVar i s of
                    T.Idx j =>
                       if j < k then
                          ()
                       else
                          raise (UncleanInternal Open)

                  | T.Term m' =>
                       cleanLoop k m' T.id)

           | T.Const _ => ()

           | T.Elim (h, spine) =>
                (
                cleanLoop k h s;
                List.app
                   (fn T.App n => cleanLoop k n s
                     | _ => ())
                   spine
                )

           | T.Lam (_, m1) =>
                cleanLoop (k+1) m1 (T.under 1 s)

           | T.Pair (m1, m2) =>
                (
                cleanLoop k m1 s;
                cleanLoop k m2 s
                )

           | T.Next m1 =>
                cleanLoop k m1 s

           | T.Triv => ()

           | T.Sub (m1, s') =>
                cleanLoop k m1 (T.compose s' s)

           | T.Evar eb =>
                (case T.readEbind eb of
                    NONE =>
                       raise (UncleanInternal NotGround)

                  | SOME m' =>
                       cleanLoop k m' s)

           | T.Native _ => ()

           | T.Marker _ =>
                raise (UncleanInternal Marker))


      fun checkClean m =
         try
            cleanLoop 0 m T.id
         with UncleanInternal prob => raise (Unclean (prob, m))



      structure IS = RedBlackSet (structure Elem = IntOrdered)

      (* freeEvarsMain (acc, set) m s

         if    set contains exactly the elements of acc
         then  l is the list of all free evars in m[s] that do not appear in set
               (in the order in which they appear)
               and
               returns (rev l @ acc, union (set, l))
      *)
      fun freeEvarsMain (accset as (acc, set))  m s =
         (case m of
             T.Var i =>
                (case T.substVar i s of
                    T.Idx j =>
                       accset

                  | T.Term m' =>
                       freeEvarsMain accset m' T.id)

           | T.Const _ =>
                accset

           | T.Elim (h, spine) =>
                List.foldl
                   (fns elim accset =>
                       (case elim of
                           T.App n =>
                              freeEvarsMain accset n s

                         | _ =>
                              accset))
                   (freeEvarsMain accset h s)
                   spine

           | T.Lam (_, m1) =>
                freeEvarsMain accset m1 (T.under 1 s)

           | T.Pair (m1, m2) =>
                freeEvarsMain (freeEvarsMain accset m1 s) m2 s

           | T.Next m1 =>
                freeEvarsMain accset m1 s

           | T.Triv => accset

           | T.Sub (m1, s') =>
                freeEvarsMain accset m1 (T.compose s' s)

           | T.Evar eb =>
                (case T.readEbind eb of
                    NONE =>
                       let
                          val i = T.ebindToInt eb
                       in
                          if IS.member set i then
                             accset
                          else
                             (eb :: acc, IS.insert set i)
                       end

                  | SOME m' =>
                       freeEvarsMain accset m' s)

           | T.Native _ => accset

           | T.Marker _ => accset)


      fun freeEvars m =
         let
            val (l, _) = freeEvarsMain ([], IS.empty) m T.id
         in
            List.rev l
         end


      val () = 
         Handler.customHandler
         (fn Prover msg => 
                (
                print msg; 
                true
                )

           | Unclean (prob, m) =>
                (
                print "Error: the term contains ";
                print 
                   (case prob of
                       NotGround => "unresolved evars"
                     | Open => "free variables"
                     | Marker => "markers");
                print ":\n\n";

                let
                   val imp = !Show.showImplicits
                in
                   Show.showImplicits := true;
                   Show.showClosed m;
                   Show.showImplicits := imp
                end;

                (case prob of
                    NotGround =>
                       (
                       print "\nUnresolved evars:";
                       List.app
                          (fn ebind =>
                              (
                              print " E";
                              print (Int.toString (T.ebindToInt ebind))
                              ))
                          (freeEvars m);
                       print "\n"
                       )
                       
                  | _ => ());

                true
                )

           | _ => false)



      fun lemma name ea =
         (case R.deref theState of
             NONE =>
                let
                   val sym = Symbol.fromValue name

                   val () =
                      (case Namespace.resolveOpt [sym] of
                          NONE => ()

                        | SOME _ =>
                             signalError (String.concat
                                               ["lemma name ", name, " already in use"]))
                   
                   val a = ETerm.closed ea
                   val jud = J.make Seq.empty a

                   val goals = [(blank, (jud, Directory.empty))]

                   fun k vs =
                      (case vs of
                          [v] =>
                             let
                                val () = checkClean a
                                val (const, jud', v') = NamespaceInternal.reify sym jud v
                             in
                                R.assign theState NONE;
                                R.assign theOriginalGoals NONE;
                                Database.setType const jud' v';
                                print "Lemma ";
                                print name;
                                print " defined.\n"
                             end

                        | _ => raise (Fail "convention violated"))
                in
                   !beforeLemmaHook ();
                   R.assign theState (SOME (0, goals, k));
                   R.assign theOriginalGoals (SOME goals);

                   Message.message 10 0 showGoals
                end
      
           | SOME _ =>
                signalError "proof underway")


      fun setGoals goals k =
         (case R.deref theState of
             NONE =>
                let
                   fun k' vs =
                      (
                      R.assign theState NONE;
                      R.assign theOriginalGoals NONE;
                      k vs
                      )
                in
                   !beforeLemmaHook ();
                   R.assign theState (SOME (0, goals, k'));
                   R.assign theOriginalGoals (SOME goals);
                   Message.message 10 0 showGoals
                end
      
           | SOME _ =>
                signalError "proof underway")


      fun openGoal goal =
         setGoals [(Message.blank, goal)] (fn _ => print "Goal complete.\n")
      


      fun typechecker () =
         action
         (fn (d, goals, k) =>
             let
                val (subgoals, validate) =
                   TypecheckInternal.multigoalTypecheck 
                      (List.map (fn (label, goal) => (Message.toString label, goal)) goals)
             in
                R.assign theState
                (SOME
                    (d, subgoals,

                     (fn vs => k (validate vs))))
             end)



      fun originalGoals () =
         (case R.deref theOriginalGoals of
             SOME goals =>
                map (fn (_, jud) => jud) goals

           | NONE => signalError "no proof underway")

      fun showOriginalGoals () =
         (case R.deref theOriginalGoals of
             SOME goals =>
                showGoalsGen 0 goals showerLong showerLong

           | NONE =>
                signalError "no proof underway")


      fun underway () = Option.isSome (R.deref theState)



      fun goals () =
         (case R.deref theState of
             NONE => signalError "no proof underway"

           | SOME (_, l, _) => map (fn (_, jud) => jud) l)

      fun currentGoal () =
         (case R.deref theState of
             NONE => signalError "no proof underway"

           | SOME (_, [], _) => signalError "no current goal"
         
           | SOME (_, (_, jud) :: _, _) => jud)

      fun currentConcl () =
         let
            val (jud, _) = currentGoal ()
         in
            J.concl jud
         end

      fun idir () =
         let
            val (_, dir) = currentGoal ()
         in
            Directory.idir dir
         end

      fun parseCurr em = ETerm.withidir (idir ()) em

      fun showCurr m =
         let
            val (_, dir) = currentGoal ()
         in
            Show.show dir m
         end


      fun checkpoint () =
         (case R.deref theState of
             NONE => !beforeLemmaHook ()

           | _ => ())

   end


structure Prover :> PROVER = ProverInternal
