
signature REORDER =
   sig

      exception Reorder of string

      (* NB: When coding a reordering, remember that a validator can be given extra validations
         at the end, which should be left alone.  You cannot assume that every validation in
         the list is for one of the goals.
      *)

      type goal = Tactic.goal

      type 'a reordering =
         ('a * goal) list 
         -> ('a * goal) list * (Refine.validation list -> Refine.validation list)


      val id : 'a reordering

      (* swap i j swaps the first i goals with the next j goals *)
      val swap : int -> int -> 'a reordering

      (* pull i = swap i 1 *)
      val pull : int -> 'a reordering

      (* push i = swap 1 i *)
      val push : int -> 'a reordering

      (* rotateForward i = swap i (n-i), where n = number of goals *)
      val rotateForward : int -> 'a reordering

      (* rotateBackward i = swap (n-i) i, where n = number of goals *)
      val rotateBackward : int -> 'a reordering

      (* reordering with (compose [r1 ... rn] is the same as reordering with r1 then r2, etc. *)
      val compose : 'a reordering list -> 'a reordering

      (* When there are n subgoals, withCount f = f n. *)
      val withCount : (int -> 'a reordering) -> 'a reordering

      (* Contract the first n+1 goals, which must be identical. *)
      val contract : int -> 'a reordering

      val sort : ('a * goal -> 'a * goal -> bool) -> 'a reordering

      val reorder : 'a Tactic.tacticm -> 'a reordering -> 'a Tactic.tacticm

   end


structure Reorder :> REORDER =
   struct

      open Tactic

      exception Reorder of string

      exception ValidationFailure = Refine.ValidationFailure

      type goal = Tactic.goal

      type 'a reordering =
         ('a * goal) list 
         -> ('a * goal) list * (Refine.validation list -> Refine.validation list)


      fun id goals = (goals, (fn vs => vs))
         
      fun swap i j goals =
         (case List.splitOpt goals i of
             NONE => raise (Reorder "not enough goals")

           | SOME (front, rest) =>
                (case List.splitOpt rest j of
                    NONE => raise (Reorder "not enough goals")

                  | SOME (back, remain) =>
                       (back @ front @ remain,
                        (fn vs =>
                            (case List.splitOpt vs j of
                                NONE => raise ValidationFailure

                              | SOME (frontvs, restvs) =>
                                   (case List.splitOpt restvs i of
                                       NONE => raise ValidationFailure

                                     | SOME (backvs, remainvs) =>
                                          backvs @ frontvs @ remainvs))))))

                                          
      fun withCount reord goals =
         reord (List.length goals) goals


      fun pull i = swap i 1

      fun push i = swap 1 i

      fun rotateForward i = withCount (fn n => swap i (n-i))

      fun rotateBackward i = withCount (fn n => swap (n-i) i)


      fun compose2 r1 r2 goals =
         let
            val (goals1, perm1) = r1 goals
            val (goals2, perm2) = r2 goals1
         in
            (goals2, fn vs => perm1 (perm2 vs))
         end

      fun compose rs = List.foldr compose2 id rs
             



      fun contractLoop jud n goals =
         if n = 0 then
            (goals, (fns _ vs => vs))
         else
            (case goals of
                [] =>
                   raise (Reorder "not enough goals")

              | (_, (jud', _)) :: rest =>
                   (case Refine.cast jud jud' of
                       NONE =>
                          raise (Reorder "contracted goals are not identical")

                     | SOME f =>
                          let
                             val (goals', g) = contractLoop jud (n-1) rest
                          in
                             (goals',
                              (fns v vs => f v :: g v vs))
                          end))

      fun contract n goals =
         if n < 0 then
            raise (Invalid "Reorder.contract")
         else
            (case goals of
                [] =>
                   raise (Reorder "not enough goals")

              | (goal as (_, (jud, _))) :: rest =>
                   let
                      val (goals', g) = contractLoop jud n rest
                   in
                      (goal :: goals',
                       (fn [] => raise ValidationFailure
                         | v :: vrest => v :: g v vrest))
                   end)



      (* just getting a value of type validation *)
      val bogusValidation =
         let
            val (_, validate) =
               Refine.refine
                  Rule.unitForm
                  (Judgement.make 
                      Seq.empty 
                      (Term.apply1 Prim.istp (Term.Const Prim.unit)))

            val (v, _) = validate []
         in
            v
         end
                  

      fun sort comp goals =
         let
            val sorted =
               Juliasort.sort' 
                  (fn ((x, _), (y, _)) => comp x y)
                  (List.mapi (fns i goal => (goal, i)) goals)

            val len = List.length goals

            fun validate vs =
               let
                  val a = Array.tabulate len (fn _ => bogusValidation)

                  fun invert l vs =
                     (case (l, vs) of
                         ([], _) => vs

                       | ((_, i) :: l', v :: vs') =>
                            (
                            Array.update a i v;
                            invert l' vs'
                            )

                       | _ =>
                            raise (Fail "precondition"))

                  val vrest = invert sorted vs

                  fun build i acc =
                     if i = 0 then
                        acc
                     else
                        build (i-1) (Array.sub a (i-1) :: acc)
               in
                  build len vrest
               end
         in
            (List.map fst sorted, validate)
         end



      fun reorder tac reord goal fk sk =
         let
            do (subgoals, validate, fk') = tac goal fk
         in
            (case
                (try
                    Sum.INL (reord subgoals)
                 with
                    Reorder msg => Sum.INR msg)
             of
                Sum.INL (subgoals', perm) =>
                   let
                      fun validate' vs = validate (perm vs)
                   in
                      sk (subgoals', validate', fk')
                   end

              | Sum.INR msg => fk msg)
         end

   end
