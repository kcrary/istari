
signature JUDGEMENT =
   sig

      type symbol = Symbol.symbol
      type term = Term.term

      datatype hyp =
         Tm of term
       | Tml of term
       | Tmh of term
       | Tmlh of term
       | Tp
       | Tpl
       | Tph
       | Let of term

      type context = hyp Seq.seq
      type judgement

      val subHyp : hyp -> Term.sub -> hyp
      val subContext : context -> Term.sub -> context

      val promote : hyp -> hyp
      val unhide : hyp -> hyp
      val promoteAndUnhide : hyp -> hyp

      val make : context -> term -> judgement

      val context : judgement -> context
      val concl : judgement -> term
      val hyp : judgement -> int -> hyp
      val hypOpt : judgement -> int -> hyp option

      (* unifyWeaken allows the use of unhidden hypotheses for hidden ones *)
      val unifyHyp : hyp -> hyp -> unit
      val unifyHypWeaken : hyp -> hyp -> unit
      val unify : judgement -> judgement -> unit
      val unifyWeaken : judgement -> judgement -> unit

      type id
      val id : judgement -> id
      val eq : id * id -> bool

      val puHyp : hyp Pickle.pu
      val puContext : context Pickle.pu

   end


structure Judgement :> JUDGEMENT =
   struct

      structure S = Seq
      structure P = Pickle

      type symbol = Symbol.symbol
      type term = Term.term

      datatype hyp =
         Tm of term
       | Tml of term
       | Tmh of term
       | Tmlh of term
       | Tp
       | Tpl
       | Tph
       | Let of term


      type id = int
      type context = hyp S.seq
      type judgement = context * term * id

      fun subHyp h s =
         (case h of
             Tm a => Tm (Term.Sub (a, s))
           | Tml a => Tml (Term.Sub (a, s))
           | Tmh a => Tmh (Term.Sub (a, s))
           | Tmlh a => Tmlh (Term.Sub (a, s))
           | Tp => Tp
           | Tpl => Tpl
           | Tph => Tph
           | Let m => Let (Term.Sub (m, s)))

      fun subContextMain hs s =
         (case S.expose hs of
             S.Nil => (S.empty, s)

           | S.Cons (h, rest) =>
                let
                   val (rest', s') = subContextMain rest s
                in
                   (S.cons (subHyp h s') rest',
                    Term.under 1 s')
                end)

      fun subContext hs s =
         let
            val (g, _) = subContextMain hs s
         in
            g
         end

      fun promote h =
         (case h of
             Tml a => Tm a
           | Tmlh a => Tmh a
           | Tpl => Tp
           | _ => h)

      fun unhide h =
         (case h of
             Tmh a => Tm a
           | Tmlh a => Tml a
           | Tph => Tp
           | _ => h)

      fun promoteAndUnhide h =
         (case h of
             Tml a => Tm a
           | Tmh a => Tm a
           | Tmlh a => Tm a
           | Tpl => Tp
           | Tph => Tp
           | _ => h)
          
      val nextJudgement = ref 0

      fun make g m =
         let
            val i = !nextJudgement
         in
            nextJudgement := i + 1;
            (g, m, i)
         end

      fun context (g, _, _) = g
      fun concl (_, m, _) = m
      
      fun hypOpt (g, _, _) i = S.nthOpt g i

      fun hyp jud i =
         (case hypOpt jud i of
             SOME h => h

           | NONE => raise (Fail "no such hypothesis"))

      
      fun unifyHyp h h' =
         (case (h, h') of
             (Tm a, Tm a') =>
                Unify.unify a a'

           | (Tml a, Tml a') =>
                Unify.unify a a'

           | (Tmh a, Tmh a') =>
                Unify.unify a a'

           | (Tmlh a, Tmlh a') =>
                Unify.unify a a'

           | (Tp, Tp) => ()

           | (Tpl, Tpl) => ()

           | (Tph, Tph) => ()

           | (Let m, Let m') =>
                Unify.unify m m'

           | _ => Unify.impossible ())

      fun unifyHypWeaken h h' =
         (case (h, h') of
             (Tm a, Tm a') =>
                Unify.unify a a'

           | (Tml a, Tml a') =>
                Unify.unify a a'

           | (Tmh a, Tmh a') =>
                Unify.unify a a'

           | (Tmh a, Tm a') =>
                Unify.unify a a'

           | (Tmlh a, Tmlh a') =>
                Unify.unify a a'

           | (Tmlh a, Tml a') =>
                Unify.unify a a'

           | (Tp, Tp) => ()

           | (Tpl, Tpl) => ()

           | (Tph, Tph) => ()

           | (Tph, Tp) => ()

           | (Let m, Let m') =>
                Unify.unify m m'

           | _ => Unify.impossible ())

      fun unifyContext g g' =
         (case (S.expose g, S.expose g') of
             (S.Nil, S.Nil) => ()

           | (S.Cons (h, rest), S.Cons (h', rest')) =>
                (
                unifyHyp h h';
                unifyContext rest rest'
                )

           | _ => Unify.impossible ())

      fun unifyContextWeaken g g' =
         (case (S.expose g, S.expose g') of
             (S.Nil, S.Nil) => ()

           | (S.Cons (h, rest), S.Cons (h', rest')) =>
                (
                unifyHypWeaken h h';
                unifyContextWeaken rest rest'
                )

           | _ => Unify.impossible ())

      fun unify (g, m, _) (g', m', _) =
         (
         unifyContext g g';
         Unify.unify m m'
         )

      fun unifyWeaken (g, m, _) (g', m', _) =
         (
         unifyContextWeaken g g';
         Unify.unify m m'
         )

      fun id (_, _, i) = i

      fun eq (i : int, j) = i = j



      val impossible = Fail "impossible"

      val puHyp =
         P.alt
            (fn Tm _ => 0
              | Tml _ => 1
              | Tmh _ => 2
              | Tmlh _ => 3
              | Tp => 4
              | Tpl => 5
              | Tph => 6
              | Let _ => 7)
            [
            P.wrap
               (fn Tm m => m
                 | _ => raise impossible)
               Tm
               Pickling.puTerm,

            P.wrap
               (fn Tml m => m
                 | _ => raise impossible)
               Tml
               Pickling.puTerm,

            P.wrap
               (fn Tmh m => m
                 | _ => raise impossible)
               Tmh
               Pickling.puTerm,

            P.wrap
               (fn Tmlh m => m
                 | _ => raise impossible)
               Tmlh
               Pickling.puTerm,

            P.const Tp,

            P.const Tpl,

            P.const Tph,

            P.wrap
               (fn Let m => m
                 | _ => raise impossible)
               Let
               Pickling.puTerm,
            ]


      fun puSeq pu =
         P.listish
            (fn () => Seq.empty)
            (fn (h, t) => Seq.cons h t)
            (fns f l => Seq.foldr (fns x () => f x) () l)
            pu


      val puContext = puSeq puHyp

   end
