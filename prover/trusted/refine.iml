
signature REFINE =
   sig

      type judgement = Judgement.judgement
      type rule = Rule.rule
      type validation

      val refine : rule -> judgement -> judgement list * (validation list -> validation * validation list)
      val id : validation -> Judgement.id
      val valid : validation -> bool

      val cast : judgement -> judgement -> (validation -> validation) option

      (* Raised when the validation function is applied to the wrong validations,
         or when a validation has been invalidated by a trail rewind.
      *)
      exception ValidationFailure

      val extracts : unit -> bool
      val extract : validation -> Term.term option
      
      (* reify J v path

         If     J is |- A  and  v validates J
         then   declares a constant (const) using v's extract as its definition
                (if extracts are supported), and path as its metadata
         and    returns (const, J', v')
         where  J' is |- const : A  and  v' validates J'

         Cleans the extract if it is not open or ground.
      *)
      val reify : judgement -> validation -> Symbol.symbol list -> Term.constant * judgement * validation


      (* Raised when extracts are enabled but the pickled validation does not contain one. *)
      exception NoExtracts

      val pu : (judgement * validation) Pickle.pu


      val disableExtracts : unit -> unit

   end


structure Refine :> REFINE =
   struct

      structure C = ConstantInternal
      structure J = Judgement
      structure T = Term

      type judgement = J.judgement
      type rule = Rule.rule

      (* term is trivial when extracts are disabled *)
      type validation = T.term * J.id * Trail.token


      val extractsOn = ref true

      
      exception ValidationFailure

      fun checkExt subgoals valids youngest acc =
         (case (subgoals, valids) of
             (nil, _) => 
                (List.rev acc, valids, youngest)

           | (subgoal :: subgoals, (m, id, tok) :: valids) =>
                if Trail.valid tok andalso J.eq (J.id subgoal, id) then
                   checkExt subgoals valids (Trail.merge youngest tok) (m :: acc)
                else
                   raise ValidationFailure

           | _ =>
                raise ValidationFailure)

      fun checkMin subgoals valids youngest =
         (case (subgoals, valids) of
             (nil, _) => 
                (valids, youngest)

           | (subgoal :: subgoals, (_, id, tok) :: valids) =>
                if Trail.valid tok andalso J.eq (J.id subgoal, id) then
                   checkMin subgoals valids (Trail.merge youngest tok)
                else
                   raise ValidationFailure

           | _ =>
                raise ValidationFailure)

      fun refineExt rule goal =
         let
            val (subgoals, txext) = Rule.apply rule goal

            val tok = Trail.token ()
            
            fun txvalid valids =
               let
                  val (exts, valids', tok') = checkExt subgoals valids tok []
               in
                  ((txext exts, J.id goal, tok'), valids')
               end
         in
            (subgoals, txvalid)
         end

      fun refineMin rule goal =
         let
            val (subgoals, _) = Rule.apply rule goal

            val tok = Trail.token ()

            fun txvalid valids =
               let
                  val (valids', tok') = checkMin subgoals valids tok
               in
                  ((T.Triv, J.id goal, tok'), valids')
               end
         in
            (subgoals, txvalid)
         end

      val refineCurr = ref refineExt

      (* avoid branching to make this slightly faster *)
      fun refine rule goal = !refineCurr rule goal



      fun id (_, x, _) = x

      fun valid (_, _, tok) = Trail.valid tok

      fun extracts () = !extractsOn

      fun extract (m, _, _) =
         if !extractsOn then
            SOME m
         else
            NONE



      fun castExt j j' =
         if J.eq (J.id j, J.id j') then
            SOME (fn v => v)
         else if (J.unifyWeaken j j'; Unify.solve ()) then
            let
               val tok = Trail.token ()
            in
               SOME (fn (m, x, tok') =>
                        let
                           val tok'' = Trail.merge tok tok'
                        in
                           if Trail.valid tok'' andalso J.eq (J.id j, x) then
                              (m, J.id j', tok'')
                           else
                              raise ValidationFailure
                        end)
            end
         else
            NONE

      fun castMin j j' =
         if J.eq (J.id j, J.id j') then
            SOME (fn v => v)
         else if (J.unifyWeaken j j'; Unify.solve ()) then
            let
               val tok = Trail.token ()
            in
               SOME (fn (_, x, tok') =>
                        let
                           val tok'' = Trail.merge tok tok'
                        in
                           if Trail.valid tok'' andalso J.eq (J.id j, x) then
                              (T.Triv, J.id j', tok'')
                           else
                              raise ValidationFailure
                        end)
            end
         else
            NONE

      (* It is possible for a validation-coercion function created with extracts were enabled
         to persist when extract are disabled.  But if that happens it's harmless.  At worst,
         an extract is kept that might otherwise be gc'ed.
      *)

      fun cast j j' =
         if !extractsOn then
            castExt j j'
         else
            castMin j j'



      fun reify j (m, id, tok) path =
         if
            Trail.valid tok andalso
            Seq.null (J.context j) andalso
            J.eq (J.id j, id)
         then
            let
               val () = Trail.commit ()

               val const =
                  if !extractsOn then
                     C.declare true (SOME m) path C.HARD
                  else
                     C.declare true NONE path C.OPAQUE

               val a = J.concl j
               val j' = J.make Seq.empty (T.apply2 Prim.ov a (T.Const const))
            in
               (const, j', (T.Triv, J.id j', tok))
            end
         else
            raise ValidationFailure



      structure P = Pickle
      structure PP = Pickling

      exception NoExtracts

      val puExt =
         P.wrap
            (fn (jud, (ext, id, tok)) =>
                if 
                   Trail.valid tok andalso
                   J.eq (J.id jud, id)
                then
                   (
                   Trail.commit ();
                   (J.context jud, J.concl jud, SOME ext)
                   )
                else
                   raise ValidationFailure)
            (fn (ctx, concl, extopt) =>
                if Unsafe.getParanoid () then
                   raise PP.Paranoid
                else
                   (case extopt of
                       SOME ext =>
                          let
                             val jud = J.make ctx concl
                             val tok = Trail.token ()
                          in
                             Trail.commit ();
                             (jud, (ext, J.id jud, tok))
                          end

                     | NONE =>
                          raise NoExtracts))
            (P.tuple3 
                J.puContext
                PP.puTerm
                (P.option PP.puTerm))

      val puMin =
         P.wrap
            (fn (jud, (_, id, tok)) =>
                if 
                   Trail.valid tok andalso
                   J.eq (J.id jud, id)
                then
                   (
                   Trail.commit ();
                   (J.context jud, J.concl jud, NONE)
                   )
                else
                   raise ValidationFailure)
            (fn (ctx, concl, _) =>
                if Unsafe.getParanoid () then
                   raise PP.Paranoid
                else
                   let
                      val jud = J.make ctx concl
                   in
                      Trail.commit ();
                      (jud, (T.Triv, J.id jud, Trail.always))
                   end)
            (P.tuple3 
                J.puContext
                PP.puTerm
                (P.option PP.puTerm))

      val pu =
         P.lift
            (fn () =>
                if !extractsOn then
                   puExt
                else
                   puMin)



      fun disableExtracts () =
         (
         extractsOn := false;
         refineCurr := refineMin
         )

   end
