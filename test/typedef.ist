
typedef
/
  datatype
    intersect (i : level) .
    forall (a : U i) .
    U i
  of
    tree : nat -> type =
    | Empty : tree 0
    | Node : forall (n : nat) . a -> forest n -> tree (succ n)
    | One : forall (n : nat) . tree n -> tree n
  
    and
    forest : nat -> type =
    | Nil : forest 0
    | Cons : forall (m n : nat) . tree m -> forest n -> forest (m + n)
/;



define /strange a/
/
  fnind strange : tree [a] __ -> nat of
  | One _ _ . 1
  | Node n _ _ . succ n
  | _ . 0

  and strange' : forall (x : forest n) . x = x : forest a n of
  | _ . ()
//
  intersect i .  forall (a : U i) n . tree a n -> nat
/;

inference.
unfold /strange/.
typecheck.
qed ();



defineInd /a/
/
  treesize : tree [a] __ -> nat of
  | Empty . 0
  | Node _ x f . succ (forestsize f)
  | One _ t . treesize t
  
  and
  forestsize : forest __ -> nat of
  | Nil . 0
  | Cons _ _ t f . treesize t + forestsize f
//
  intersect i . forall (a : U i) n . tree a n -> nat
  and
  intersect i . forall (a : U i) n . forest a n -> nat
/;



lemma "tree_case"
/
  forall i (a : U i) n (t : tree a (succ n)) .
    0 < (case t : tree of
         | Empty . 0
         | _ . 1)
/;

inference.
intro /i a n t/.
destruct /t/ /{}/.
  {
  omega.
  }

  {
  reduce //.
  omega.
  }

  {
  reduce //.
  omega.
  }
qed ();



lemma "tree_case_annot"
/
  forall i (a : U i) n (t : tree a (succ n)) .
    (case t : tree :> U i of
     | Empty . void
     | _ . unit)
/;

inference.
intro /i a n t/.
destruct /t/ /{}/.
  {
  omega.
  }

  {
  reduce //.
  split.
  }

  {
  reduce //.
  split.
  }
qed ();



lemma "destructThin"
/
  forall i (a : U i) n (t : tree a (succ n)) . t = t : tree a (succ n) -> unit
/;

inference.
intro /i a n t H/.
destructThin /t/ /{x f | t}/.
  {
  injection /H/.
  split.
  }

  {
  injection /H/.
  split.
  }
qed ();



lemma "inversion"
/
  forall i (a : U i) n (t : tree a (succ n)) . t = t : tree a (succ n) -> unit
/;

inference.
intro /i a n t H/.
inversion /t/.
  {
  reintro /x f/.
  split.
  }

  {
  reintro /t'/.
  split.
  }
qed ();



lemma "destructThin2"
/
  forall i (a : U i) n (t : tree a n) . t = t : tree a n -> unit
/;

inference.
intro /i a n t H/.
destructThin /t/ /{Heq | n' x f Heq | t}/.
  {
  split.
  }

  {
  moveBefore /n'/ /n/.
  subst /n/.
  injection /H/.
  split.
  }

  {
  injection /H/.
  split.
  }
qed ();



lemma "inversion2"
/
  forall i (a : U i) n (t : tree a n) . t = t : tree a n -> unit
/;

inference.
intro /i a n t H/.
inversion /t/.
  {
  reintro /Heq/.
  split.
  }

  {
  reintro /m x f Heq/.
  split.
  }

  {
  reintro /t'/.
  split.
  }
qed ();



lemma "reduce_iterator"
/
  forall (i : level) (a : U i) n (x : tree a n) .
    treesize a n x = n : nat
/;

inference.
intro /i a n x/.
apply /tree_iter _ (fn n x . treesize a n x = n : nat) (fn n x . forestsize a n x = n : nat)/ >> reduce // >> clear /n x/.
  {
  reflexivity.
  }

  {
  intro /n x y IH/.
  compat.
  hyp /IH/.
  }

  {
  intro /n x IH/.
  hyp /IH/.
  }

  {
  reflexivity.
  }

  {
  intro /m n x IH1 y IH2/.
  compat.
    {
    hyp /IH1/.
    }

    {
    hyp /IH2/.
    }
  }
qed ();



lemma "iterate_tree"
/
  forall (i : level) (a : U i) n (x : tree a n) (extra : a) .
    x = x : tree a n
/;

inference.
intro /i a n x extra/.
iterate /x/.
5:{
  change /concl/ /(fn n x . x = x : forest a n) : _/.
  typecheck.
  }

  {
  reflexivity.
  }

  {
  intro /n z x IH/.
  compat.
  }

  {
  intro /n x IH/.
  compat.
  }

  {
  reduce //.
  reflexivity.
  }

  {
  intro /m n x IH1 y IH2/.
  compat.
  }
qed ();



lemma "destruct_tree"
/
  forall (i : level) (a : U i) n (x : tree a n) (extra : a) .
    x = x : tree a n
/;

inference.
intro /i a n x extra/.
induct /x/.
reintro /IH/.
destruct /x/ /{ Heq | m x y Heq | m x Heq}/.
  {
  reflexivity.
  }

  {
  reflexivity.
  }

  {
  subst /m/.
  fold /pause x/.
  compat.
  unfold /pause/.
  apply /IH/.
  apply /One_subterm/.
  }
qed ();



lemma "inject_tree_no"
/
  forall (i : level) (a : U i) (n : nat) (x : a) (f : forest a n) .
    Node _ _ x f = One _ _ (Node _ _ x f) : tree a (succ n)
    -> void
/;

inference.
intro /i a n x f H/.
injection /H/.
qed ();



lemma "no_fixpoint_One"
/
  forall (i : level) (a : U i) (n : nat) (t : tree a n) .
    t = One _ _ t : tree a n
    -> void
/;

inference.
intro /i a n t/.
iterate /t/.
5:{
  change /concl/ /(fn _ _ . unit) : _/.
  typecheck.
  }

  {
  intro /H/.
  injection /H/.
  }

  {
  reduce //.
  intro /n x f _ H/.
  injection /H/.
  }

  {
  intro /n t IH H/.
  apply /IH/.
  injection /H/.
  reintro /_ Heq/.
  hyp /Heq/.
  }

  {
  reduce //.
  auto.
  }

  {
  reduce //.
  nauto 6.
  }
qed ();



lemma "a_tree"
/
  forall (i : level) (a : U i) (x : a) .
    One _ _ (Node _ _ x (Cons _ _ _ (Empty _) (Nil _))) : tree _ _
/;

inference.
intro /i a x/.
typecheck.
qed ();



typedef
/
  datatype
    intersect (i : level) .
    forall (a : nat -> U i) .
    U i
  of
    foo : forall (n : nat) . a n -> a n -> type =
    | fooi : forall (x : a 0) . foo 0 x x
/;



lemma "iterate_foo"
/
  forall (i : level) (a : nat -> U i) n (x y : a n) (z : foo a n x y) (extra : a 0) .
    z = z : foo a n x y
/;

inference.
intro /i a n x y z extra/.
revert /n x y z/.
apply /foo_iter a (fn n x y z . z = z : foo a n x y)/.
reduce //.
intro /x/.
reflexivity.
qed ();



lemma "destruct_foo"
/
  forall (i : level) (a : nat -> U i) n (x y : a n) (z : foo a n x y) (extra : a 0) .
    z = z : foo a n x y
/;

inference.
intro /i a n x y z extra/.
destruct /z/ /{m Heq1 Heq2 Heq3}/.
reflexivity.
qed ();



typedef
/
  datatype U 0
  of
    exp : type =
    | scomb : exp
    | kcomb : exp
    | icomb : exp
    | app : exp -> exp -> exp 
/;



typedef
/
  datatype U 0
  of
    tp : type =
    | o : tp
    | arr : tp -> tp -> tp
/;



typedef
/
  datatype U 0
  of
    dof : exp -> tp -> type =

    | dof_scomb :
        forall (a b c : tp) .
          dof scomb 
            (arr
               (arr c (arr a b))
               (arr
                  (arr c a)
                  (arr c b)))

    | dof_kcomb :
        forall (a b : tp) .
          dof kcomb (arr a (arr b a))

    | dof_icomb :
        forall (a : tp) .
          dof icomb (arr a a)

    | dof_app :
        forall (a b : tp) (m n : exp) .
          dof m (arr a b)
          -> dof n a
          -> dof (app m n) b
/;



lemma "kxy"
/
  forall a b x y .
    dof x a
    -> dof y b
    -> dof (app (app kcomb x) y) a
/;

inference.
intro /a b x y Hx Hy/.
apply /dof_app b/.
  {
  apply /dof_app/.
    {
    apply /dof_kcomb/.
    }
  typechecker ();
  hyp /Hx/.
  }

  {
  hyp /Hy/.
  }
qed ();
